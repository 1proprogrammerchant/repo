# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-nilearn
_pkgname=nilearn
pkgver=0.9.0
pkgrel=1
pkgdesc='Machine learning for NeuroImaging in Python'
arch=('any')
url='https://github.com/nilearn/nilearn'
license=('BSD')
depends=(
  'python-joblib'
  'python-matplotlib'
  'python-nibabel'
  'python-numpy'
  'python-pandas'
  'python-pillow'
  'python-pydicom'
  'python-scikit-learn'
  'python-scipy'
)
checkdepends=(
  'python-coverage'
  'python-nose'
  'python-pytest'
)
makedepends=(
  'cython'
  'python-setuptools'
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/nilearn/nilearn/archive/${pkgver}.tar.gz")
sha512sums=('29faf04149c4baf8709c7214c0a315ba3d882563f38fb2ecbb33c942c6eee74fadc96bc8415b5421b7642748805160052a5cf88d74ea8f601c294c77907b3da3')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  pytest -v
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
