# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-nilearn
_pkgname=nilearn
pkgver=0.10.1
pkgrel=1
pkgdesc='Machine learning for NeuroImaging in Python'
arch=('any')
url='https://github.com/nilearn/nilearn'
license=('BSD')
depends=(
  'python-joblib'
  'python-matplotlib'
  'python-nibabel'
  'python-numpy'
  'python-pandas'
  'python-pillow'
  'python-pydicom'
  'python-scikit-learn'
  'python-scipy'
)
checkdepends=(
  'python-coverage'
  'python-nose'
  'python-pytest'
)
makedepends=(
  'cython'
  python-build
  python-installer
  python-wheel
  python-hatchling
  python-hatch-vcs
)
source=("${_pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('04a598ac58e16060622f14ae84467829ecf693f3f362d94333e24083c3434b58800a6199c5a1de5dcd29fa34c28e34f04228b6882291d1f0d7ea08ba00729d1a')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
