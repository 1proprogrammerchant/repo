# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=pypdf
pkgname=python-pypdf
pkgver=3.12.2
pkgrel=1
pkgdesc='A utility to read and write PDFs with Python (v3+)'
arch=('any')
url='https://github.com/py-pdf/pypdf'
license=('BSD')
depends=(
  python
)
optdepends=(
  python-pillow
  python-pycryptodome
)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/py-pdf/pypdf/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('b888c81cc3cb331f7b5709c4fa29c9f2f0790fc67679112de9e48f31cb20047b7622bcb8e555d5113e6dea96ec5d1c2759ac84696278123eb5a7faeea727b192')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
