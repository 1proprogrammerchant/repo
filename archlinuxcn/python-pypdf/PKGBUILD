# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=pypdf
pkgname=python-pypdf
pkgver=3.4.0
pkgrel=1
pkgdesc='A utility to read and write PDFs with Python (v3+)'
arch=('any')
url='https://github.com/py-pdf/pypdf'
license=('BSD')
depends=(
  python
)
optdepends=(
  python-pillow
  python-pycryptodome
)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/py-pdf/pypdf/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('9bee70a039c42533b600f1e95e28a4889d7df212bae18deca7d88501008d3a3e2b4d7fe36134e947cb14b0acc650126688c358ce2b662917a4c6df46241db1ab')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
