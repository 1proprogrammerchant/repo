# Maintainer: Yidaozhan Ya <yidaozhan_ya@outlook.com>
# Maintainer: Dct Mei <dctxmei@yandex.com>

pkgname=v2raya-tray-git
_pkgname=v2raya-tray
pkgver=1.0
pkgrel=1
pkgdesc="Operate v2rayA in the system tray (git version)"
arch=('any')
url="https://github.com/YidaozhanYa/v2rayATray"
license=('MIT')
depends=('python' 'python-pyqt5')
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}"/"${_pkgname}"/
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    depends+=('v2raya')
    cd "${srcdir}"/"${_pkgname}"/
    install -Dm0644 LICENSE -t "${pkgdir}"/usr/share/licenses/"${_pkgname}"/
    make DESTDIR="${pkgdir}" install
}
