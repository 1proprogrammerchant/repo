# Maintainer: axionl <axionl@aosc.io>
# Contributor: Sherlock Holo <sherlockya(at)gmail.com>
pkgname=filebrowser-bin
pkgver=2.19.0
pkgrel=2
pkgdesc="Web File Manager which can be used as a middleware or standalone app."
arch=('x86_64' 'aarch64')
url="https://github.com/filebrowser/filebrowser"
license=('Apache')
depends=('glibc')
provides=("filebrowser")
conflicts=("filebrowser")
backup=("etc/filebrowser/filebrowser.conf")
install="${pkgname}.install"

source=('filebrowser.service' 'filebrowser@.service' 'filebrowser-bin.install'
        'filebrowser.sysusers' 'filebrowser.tmpfiles'
        'https://raw.githubusercontent.com/hacdias/filebrowser/master/LICENSE')
source_x86_64=("linux-amd64-filebrowser-${pkgver}.tar.gz::https://github.com/filebrowser/filebrowser/releases/download/v${pkgver}/linux-amd64-filebrowser.tar.gz")
source_aarch64=("linux-aarch64-filebrowser-${pkgver}.tar.gz::https://github.com/filebrowser/filebrowser/releases/download/v${pkgver}/linux-arm64-filebrowser.tar.gz")

sha256sums=('441dd85212c4b66c54c02aba2e64ef994f514c534b42a1a0dd0387124c7064a7'
            '3e3007a8cc00f05e0a51f1aa2c3ca66afc5ebc5aadcca6ef977f33862af08833'
            '434a0f1c2b02f858008bd252d30152510a692d66dbcd65604ae3c1e16344a377'
            '6246fbeac57750e146216892ed6eb1d43a995f987bb89ef12bcbfb3963f5aa2d'
            'fd3da1de58e98185e0043070f55b46a86a78b9ebcdda949d0cd8a83e9b0b230c'
            '1fc20cab3a7d67d7997126a98dd151a362dc4600201ca37fd608b959d25985db')
sha256sums_x86_64=('549bf4f03c0de9ef27b0527900fafdd412e3fbaecb9fcdc8a1dbe357a06e05a8')
sha256sums_aarch64=('67be1fd9c313521da359462ddf92d14d5b084d94d20ec0dc7942648d97705f00')

package() {
    install -Dm644 "${srcdir}/filebrowser.sysusers" "${pkgdir}/usr/lib/sysusers.d/filebrowser.conf"
    install -Dm644 "${srcdir}/filebrowser.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/filebrowser.conf"
    install -Dm644 "filebrowser.servic"e -t "${pkgdir}/usr/lib/systemd/system/"
    install -Dm644 "filebrowser@.service" -t "${pkgdir}/usr/lib/systemd/system/"
    install -Dm755 "LICENSE" -t "${pkgdir}/usr/share/licenses/filebrowser-bin/"
    install -Dm755 "${srcdir}/filebrowser" -t "${pkgdir}/usr/bin/"
}

# vim set: ts=4 sw=4 et:
