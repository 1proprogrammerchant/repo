# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=ITKPythonPackage
pkgname=python-itk
pkgver=5.2.0
pkgrel=1
pkgdesc='An open-source toolkit for multidimensional image analysis. This package provides only the python binding'
arch=('x86_64')
url='https://itk.org/'
license=('Apache')
depends=(
  gcc-libs
  python-numpy
)
makedepends=(
  clang
  doxygen
  git
  python-setuptools
  python-scikit-build
)

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/InsightSoftwareConsortium/ITKPythonPackage/archive/v${pkgver}.tar.gz"
"0001-fix-build-with-cmake-3.20.patch::https://github.com/InsightSoftwareConsortium/ITKPythonPackage/pull/156.patch"
"ITK-${pkgver}.tar.gz::https://github.com/InsightSoftwareConsortium/ITK/archive/refs/tags/v${pkgver}.tar.gz"
"0002-fix-ipo.patch::https://github.com/InsightSoftwareConsortium/ITK/pull/2498.patch"
)
sha512sums=('d6ef9597c52b349d9134752cd0236e9d53bdafd4e85edc65ea09be982b58b0f4ac56e7e9b49c790505a17f32148e53122a118a497fdb3f3190afb2ded03efa59'
            '8901af2ccc5283c8499c56fd12059d08ff3865d689861d4055cd2493bb89e1ced2439a045e69157dc59db7f48d65f858158fe695bf4df514e4eba0984edd26ab'
            'a055280f37ce187d29bc5ed4c6732094d4285b583ec7874a193bea90da2a455c9cb514e313fe20c65b5f65821eae6040a86815d07450e82498509bc1e4cd8d3b'
            '460b8431ed4f47d0c3afa83fa195a8520c049c9e9e038d7d1cfd3935d2eff1373b07e4a5ba9796a917788cd2dff78f8c6ddd53b0ee7643355241e8b85266e027')

prepare() {
  cd "${_pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-fix-build-with-cmake-3.20.patch"
  cd "${srcdir}/ITK-${pkgver}"
  patch -p1 -i "${srcdir}/0002-fix-ipo.patch"
  export CC=clang
  export CXX=clang++
}

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build -- \
    -DITK_WRAP_unsigned_short:BOOL=ON \
    -DITK_WRAP_double:BOOL=ON \
    -DITK_WRAP_IMAGE_DIMS:STRING="2;3;4" \
    -DITK_SOURCE_DIR="${srcdir}/ITK-${pkgver}"
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
# vim:set ts=2 sw=2 et:

