# Maintainer: Ariel AxionL <i at axionl dot me>
pkgname=maddy
pkgver=0.5.2
pkgrel=6
pkgdesc='Composable all-in-one mail server'
arch=('x86_64')
url="https://github.com/foxcpp/maddy"
license=('GPL3')
depends=('glibc' 'pam')
makedepends=('go' 'git' 'scdoc')
backup=("etc/maddy/maddy.conf")
provides=("maddy" "maddyctl" "maddy-pam-helper" "maddy-shadow-helper")
source=("https://github.com/foxcpp/maddy/releases/download/v${pkgver}/${pkgname}-${pkgver}-src.tar.zst"{,.sig}
    "maddy.sysusers"
    "maddy.tmpfiles"
    "maddy.service"
    "maddy@.service")

b2sums=('e44d93a12e80d3aeb278ca7b4f554f26cc140bdf1a58bc5e04ad4e81a512d00f50bf16a213e11b78b2725ecd9ed2ed189032ac1b777b8cff46d6afc8fdb0f5f9'
        'SKIP'
        '2e081425075312f12f3e0a82906016787b195426fa1bd7b664e3eef020407a10fa791ce6af8db30f86c72f0b2b4784c151e1d687c6705d110f965e0dd3d154ac'
        '44fa4cc5ccd034b939a5800ea3745652500457914470f93374bf4304cd19e5fd5e315b651d5647fa48d570f3880c8a609deddbbdf63a8c9d6432e79dd4e310a5'
        'e7ee65ebc3e3e5578a65bb5abddd4668b1ca16948da64d1f2215b2ccdf0733897514f23b632d8e30e88f14139eb2bbbc3350abf4bb0c88fdf26b65f592163f54'
        '8af5d78df25a4ba37c80207acb5c39335adc6427157d75dccdcf203c250434c2b01b0eb62ac6cbd806e0c9d13e3a855902720665253119b68e88707357451d0c')

validpgpkeys=("3197BBD95137E682A59717B434BB2007081396F4")

prepare(){
  mkdir -p "gopath"
  cd "${pkgname}-${pkgver}-src"
  mkdir -p "build"
}

build() {
  export GOPATH="${srcdir}/gopath"
  chmod -R +w ${GOPATH}

  cd "${pkgname}-${pkgver}-src"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags "-extldflags ${LDFLAGS} -s -w -X github.com/foxcpp/maddy.Version=${pkgver}" -x -o build ./cmd/...
}

check() {
  cd "${pkgname}-${pkgver}-src"
  go test ./...
}

package() {
  install -Dm644 "${srcdir}/maddy.sysusers" -t "${pkgdir}/usr/lib/sysusers.d/"
  install -Dm644 "${srcdir}/maddy.tmpfiles" -t "${pkgdir}/usr/lib/tmpfiles.d/"
  install -Dm644 "${srcdir}/maddy.service" -t "${pkgdir}/usr/lib/systemd/system/"
  install -Dm644 "${srcdir}/maddy@.service" -t "${pkgdir}/usr/lib/systemd/system/"

  cd "${pkgname}-${pkgver}-src"

  local targets=("maddy" "maddyctl" "maddy-pam-helper" "maddy-shadow-helper")
  for _binary in "${targets[@]}"; do
    install -Dm755 "build/${_binary}" -t "${pkgdir}/usr/bin/"
  done

  install -Dm644 "docs/man/maddy.1.scd" "${pkgdir}/usr/share/man/man1/maddy.1"
  local docs=("maddy" "maddy-auth" "maddy-blob" "maddy-config" "maddy-filters" "maddy-imap" "maddy-smtp" "maddy-storage" "maddy-tables" "maddy-targets" "maddy-tls")
  for _doc in "${docs[@]}"; do
      install -Dm644 "docs/man/${_doc}.5.scd" "${pkgdir}/usr/share/man/man5/${_doc}.5"
  done

  install -Dm600 "maddy.conf" -t "${pkgdir}/etc/maddy/"
}

# vim:set ts=2 sw=2 et:
