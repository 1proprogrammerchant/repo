# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=castxml
pkgdesc="C-family abstract syntax tree XML output tool."
pkgver=0.6.0
pkgrel=1
arch=('x86_64')
url='https://github.com/CastXML/CastXML'
license=('Apache')
depends=(
  clang
  llvm-libs
)
makedepends=(
  cmake
  git
  llvm
  python-sphinx
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/CastXML/CastXML/archive/v${pkgver}.tar.gz")
sha512sums=('5ea3c163444e89bfaca068c329787a781f379a743b59b802bda9fec8c0f3cbcaf4315c80027ffc149cd3899b72a258484a8c7ced406e41825d76679b29a72eff')

build() {
  cmake -B "build" -S "${srcdir}/CastXML-${pkgver}" \
    -DCastXML_INSTALL_DOC_DIR=share/doc/castxml \
    -DCastXML_INSTALL_MAN_DIR=share/man \
    -DCLANG_LINK_CLANG_DYLIB=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH=ON \
    -DSPHINX_HTML=ON \
    -DSPHINX_MAN=ON
  make -C "build"
}

check() {
  cd "build"
  ctest -v
}

package(){
  cd "build"
  make DESTDIR="${pkgdir}" install
}
# vim:set ts=2 sw=2 et:

