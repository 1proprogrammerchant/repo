# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=castxml
pkgdesc="C-family abstract syntax tree XML output tool."
pkgver=0.4.6
pkgrel=2
arch=('x86_64')
url='https://github.com/CastXML/CastXML'
license=('Apache')
depends=(
  clang
  llvm-libs
)
makedepends=(
  cmake
  git
  llvm
  python-sphinx
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/CastXML/CastXML/archive/v${pkgver}.tar.gz")
sha512sums=('8ecf0e757b5885a3efbf3f47528aad8bb4d9b498beafb3579ba67ef532d4cfab4637d85281c01c6da91f1c778876608d9a0706937d83df859abe8e20ec7f16ac')

build() {
  cmake -B "build" -S "${srcdir}/CastXML-${pkgver}" \
    -DCastXML_INSTALL_DOC_DIR=share/doc/castxml \
    -DCastXML_INSTALL_MAN_DIR=share/man \
    -DCLANG_LINK_CLANG_DYLIB=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH=ON \
    -DSPHINX_HTML=ON \
    -DSPHINX_MAN=ON
  make -C "build"
}

check() {
  cd "build"
  ctest -v
}

package(){
  cd "build"
  make DESTDIR="${pkgdir}" install
}
# vim:set ts=2 sw=2 et:

