# Maintainer of this PKGBUILD file: Martino Pilia <martino.pilia@gmail.com>

_pkgname=pynrrd
pkgname=python-pynrrd
pkgver=0.4.3
pkgrel=1
pkgdesc='Simple pure-python module for reading and writing nrrd files'
arch=('any')
url='https://github.com/mhe/pynrrd'
license=('MIT')
depends=(
  python-numpy
)
makedepends=(
  python-setuptools
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/mhe/pynrrd/archive/v${pkgver}.tar.gz")
sha256sums=('0fba1c97ec2fc6a60ef1a56eec8f47d5f570168d22b4e8763565ff93f5366c27')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_pkgname}-${pkgver}"
  python -m unittest discover -v nrrd/tests
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
