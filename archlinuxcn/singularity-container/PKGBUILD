# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Maciej Sieczka <msieczka at sieczka dot org>

_pkgname=singularity
pkgname=singularity-container
pkgver=3.8.0
pkgrel=2
pkgdesc='Application containers for Linux'
arch=('x86_64')
url='https://www.sylabs.io/singularity/'
license=('BSD')
makedepends=(
  go
)
depends=(
  cryptsetup
  libseccomp
  squashfs-tools
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hpcng/singularity/releases/download/v${pkgver}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('e9608b0e0a8c805218bbe795e9176484837b2f7fcb95e5469b853b3809a2412e')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  cd "${_pkgname}-${pkgver}"
  ./mconfig \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --prefix=/usr \
    --sysconfdir=/etc \
    -P release-stripped \
    -V ${pkgver} \
    -v
  make -C builddir
}

package() {
  make -C "${_pkgname}-${pkgver}/builddir" DESTDIR="${pkgdir}" install man
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/LICENSE.md" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:

