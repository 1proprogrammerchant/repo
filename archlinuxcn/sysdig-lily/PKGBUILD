# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contribuitor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgname=sysdig-lily
pkgver=0.28.0
pkgrel=11

# from src/sysdig-*/cmake/modules/falcosecurity-libs.cmake
_falcover=2160111cd088aea9ae2235d3385ecb0b1ab6623c

pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('x86_64')
url="https://www.sysdig.com/"
license=('GPL2' 'Apache' 'MIT')
depends=('jsoncpp' 'libjsoncpp.so' 'luajit' 'curl' 'libcurl.so' 'jq' 'libb64' 'intel-tbb' 'grpc' "linux-lily=5.15.26-2" 'libgrpc++_unsecure.so')
makedepends=('cmake' 'llvm' 'clang' 'gtest' 'linux-lily-headers')
source=("https://github.com/draios/sysdig/archive/$pkgver/sysdig-$pkgver.tar.gz"
        "https://github.com/falcosecurity/libs/archive/$_falcover.tar.gz"
        "bashcomp-location.patch"
        "falcosecurity-libs-nodownload.patch"
        "falcosecurity-liblua-2.1.patch")
sha256sums=('817e595d501acf3fe11dc659a154aadbba848dd87ae801f5bf34a42e84979f37'
            'f84edc4f7490064a0e2264594013c01c205bc5fc968376bfb0ecc17582e5e112'
            'aaee8a0ff414a24c5d5a479229324be1667bc5eb70702838f5d617fd986f947b'
            'b8cedef6766c691a8a92810eb459593d80070f0cf5dfc32cae2a80ec00639429'
            '11863751dc555a2a406e5276bbafa9185f1031d6f12c4cb051ac7097e7467889')
conflicts=(sysdig)
provides=(sysdig=$pkgver)

prepare() {
  cd "$srcdir"/sysdig-$pkgver
  patch -p1 -i "$srcdir"/bashcomp-location.patch
  patch -p1 -i "$srcdir"/falcosecurity-libs-nodownload.patch

  cd "$srcdir/libs-$_falcover"
  patch -p1 -i "$srcdir"/falcosecurity-liblua-2.1.patch
}

build() {
  cd "$srcdir"/sysdig-$pkgver
  rm -rf build
  mkdir build
  cd build

  export KERNELDIR=/usr/src/linux-lily
  cmake .. \
        -DCMAKE_VERBOSE_MAKEFILE:BOOL=ON \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_CXX_FLAGS="${CXXFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations" \
        -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSDIG_VERSION=$pkgver \
        -DUSE_BUNDLED_DEPS=OFF \
        -DBUILD_DRIVER=ON \
        -DBUILD_BPF=ON \
        -DENABLE_DKMS=OFF \
        -DBUILD_LIBSCAP_EXAMPLES=OFF
  make
}

package() {
  cd "$srcdir"/sysdig-$pkgver

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 NOTICES COPYING "$pkgdir"/usr/share/licenses/$pkgname

  cd build
  make install DESTDIR="$pkgdir"

  _extradir="/usr/lib/modules/$(</usr/src/linux-lily/version)/extramodules"
  install -Dt "${pkgdir}${_extradir}" -m644 driver/src/*.ko
  find "${pkgdir}" -name '*.ko' -exec xz {} +

  rm -r "$pkgdir"/usr/share/zsh/vendor-completions/

  rm -r "$pkgdir"/usr/src/
  mkdir -p "$pkgdir"/usr/src/sysdig-${pkgver}/bpf/
  install -Dm644 "$srcdir"/sysdig-$pkgver/build/driver/bpf/probe.o "$pkgdir"/usr/src/sysdig-${pkgver}/bpf/
  sed -i '/probe\.o/s/\<mv\>/cp --reflink=auto/' "$pkgdir"/usr/bin/scap-driver-loader
}
