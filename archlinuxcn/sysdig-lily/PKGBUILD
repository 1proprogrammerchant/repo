# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contribuitor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgname=sysdig-lily
pkgver=0.27.1
pkgrel=11
pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('x86_64')
url="https://www.sysdig.com/"
license=('GPL2' 'Apache' 'MIT')
depends=('jsoncpp' 'libjsoncpp.so' 'luajit' 'curl' 'libcurl.so' 'jq' 'libb64' 'intel-tbb' 'grpc' "linux-lily=5.10.71-1" 'libgrpc++_unsecure.so')
makedepends=('cmake' 'pandoc' 'gtest' 'linux-lily-headers')
source=("https://github.com/draios/sysdig/archive/$pkgver/sysdig-$pkgver.tar.gz"
        "filters-fix.patch"
        "bashcomp-location.patch")
sha256sums=('227eea9ffb65829c079fed379f4830fcb3b3a1ae597aa10d639fb49273afb18c'
            '36ec117b4556dfed08313d25f5124b22d0f73f7aeb7df68da9a99f987f929eda'
            'aaee8a0ff414a24c5d5a479229324be1667bc5eb70702838f5d617fd986f947b')
conflicts=(sysdig)
provides=(sysdig=$pkgver)

prepare() {
  cd "$srcdir"/sysdig-$pkgver
  patch -p1 -i "$srcdir"/bashcomp-location.patch
  patch -p1 -i "$srcdir"/filters-fix.patch
}

build() {
  cd "$srcdir"/sysdig-$pkgver
  rm -rf build
  mkdir build
  cd build
  export KERNELDIR=/usr/src/linux-lily
  cmake .. \
        -DCMAKE_VERBOSE_MAKEFILE:BOOL=ON \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_CXX_FLAGS="${CXXFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations" \
        -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSDIG_VERSION=$pkgver \
        -DUSE_BUNDLED_DEPS=OFF \
        -DBUILD_DRIVER=ON \
        -DENABLE_DKMS=OFF \
        -DBUILD_LIBSCAP_EXAMPLES=OFF
  make

  ln -srf driver/src/driver_config.h ../driver
  cd ../driver/bpf
  make
}

package() {
  cd "$srcdir"/sysdig-$pkgver

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 NOTICES COPYING "$pkgdir"/usr/share/licenses/$pkgname

  cd build
  make install DESTDIR="$pkgdir"

  _extradir="/usr/lib/modules/$(</usr/src/linux-lily/version)/extramodules"
  install -Dt "${pkgdir}${_extradir}" -m644 driver/src/*.ko
  find "${pkgdir}" -name '*.ko' -exec xz {} +

  rm -r "$pkgdir"/usr/share/zsh/vendor-completions/

  rm -r "$pkgdir"/usr/src/
  mkdir -p "$pkgdir"/usr/src/sysdig-${pkgver}/bpf/
  install -Dm644 "$srcdir"/sysdig-$pkgver/driver/bpf/probe.o "$pkgdir"/usr/src/sysdig-${pkgver}/bpf/
  sed -i '/probe\.o/s/\<mv\>/cp --reflink=auto/' "$pkgdir"/usr/bin/sysdig-probe-loader
}
