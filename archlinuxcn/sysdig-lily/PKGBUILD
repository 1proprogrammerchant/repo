# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contribuitor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgname=sysdig-lily
pkgver=0.31.5
pkgrel=2

# from src/sysdig-*/cmake/modules/falcosecurity-libs.cmake
_falcover=0.11.0

pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('x86_64')
url="https://www.sysdig.com/"
license=('GPL2' 'Apache' 'MIT')
depends=('jsoncpp' 'libjsoncpp.so' 'luajit' 'curl' 'libcurl.so' 'jq' 'libb64' 'intel-tbb' 'grpc' 'libgrpc++.so' "linux-lily=6.3.5-1" 'yaml-cpp' 'zstd')
makedepends=('cmake' 'llvm' 'clang' 'gtest' 'linux-lily-headers')
source=("https://github.com/draios/sysdig/archive/$pkgver/sysdig-$pkgver.tar.gz"
        "https://github.com/draios/sysdig/compare/35ded9aab87801281e22898242e24e0bc63873b2...954e6fc6238f21d4870a491395d68a7dd3062aa9.patch"
        "falcosecurity-libs-$_falcover.tar.gz::https://github.com/falcosecurity/libs/archive/$_falcover.tar.gz"
        "bashcomp-location.patch"
        "falcosecurity-libs-nodownload.patch"
        "libsinsp-zstd.patch")
sha256sums=('9af98cae7c38273f7429ba0df628c9745bd92c949f444e180b9dd800af14c6dd'
            '02184e3c470c90ccf94defbc04e302e87bbcf669831a5295bc2162c1292eec30'
            '90414d00591986e94f2c49e7d65eef5c2dedee266354dd3db8234bb3b06c86fa'
            '3b659326176c314eee9115adac39a249dc4b9530511b344ea6a2b23236bb8386'
            '3392204c265ef46c2a1378fc2acbb74b2b440585de4c9127a007f97ce10f0cfa'
            'b966998a576310ced927628c2b9e860ca7ab31cac7061b1d5339c2fa0398e236')
conflicts=(sysdig)
provides=(sysdig=$pkgver)

prepare() {
  cd "$srcdir/libs-$_falcover"
  patch -p1 -i "$srcdir"/libsinsp-zstd.patch

  cd "$srcdir/sysdig-$pkgver"
  patch -p1 -i "$srcdir"/35ded9aab87801281e22898242e24e0bc63873b2...954e6fc6238f21d4870a491395d68a7dd3062aa9.patch
  patch -p1 -i "$srcdir"/bashcomp-location.patch
  patch -p1 -i "$srcdir"/falcosecurity-libs-nodownload.patch

  sed s/USE_BUNDLED_DEPS/USE_BUNDLED_NJSON/ -i cmake/modules/nlohmann-json.cmake
# Fix build with abseil-cpp 2023
  sed -e 's|-std=c++0x||' -i CMakeLists.txt -i ../libs-${_falcover}/cmake/modules/CompilerFlags.cmake
}

build() {
  cd "$srcdir"/sysdig-$pkgver
  rm -rf build
  mkdir build
  cd build

  export KERNELDIR=/usr/src/linux-lily
  cmake .. \
        -DCMAKE_VERBOSE_MAKEFILE:BOOL=ON \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_CXX_FLAGS="${CXXFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations" \
        -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations" \
        -DCMAKE_CXX_STANDARD=17 \
        -DCMAKE_CXX_EXTENSIONS=OFF \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSDIG_VERSION=$pkgver \
        -DDRIVER_VERSION=$_falcover \
        -DUSE_BUNDLED_DEPS=OFF \
        -DUSE_BUNDLED_TBB=OFF \
        -DUSE_BUNDLED_B64=OFF \
        -DUSE_BUNDLED_JSONCPP=OFF \
        -DUSE_BUNDLED_RE2=OFF \
        -DUSE_BUNDLED_VALIJSON=ON \
        -DUSE_BUNDLED_NJSON=ON \
        -DBUILD_DRIVER=ON \
        -DBUILD_BPF=ON \
        -DENABLE_DKMS=OFF \
        -DBUILD_LIBSCAP_EXAMPLES=OFF
  make
}

package() {
  cd "$srcdir"/sysdig-$pkgver

  cd build
  make install DESTDIR="$pkgdir"

  _extradir="/usr/lib/modules/$(</usr/src/linux-lily/version)/extramodules"
  install -Dt "${pkgdir}${_extradir}" -m644 driver/src/*.ko
  find "${pkgdir}" -name '*.ko' -exec xz {} +

  rm -r "$pkgdir"/usr/share/zsh/vendor-completions/

  rm -rf "$pkgdir"/usr/src "$pkgdir"/sysdig
  mkdir -p "$pkgdir"/usr/src/sysdig-${pkgver}/bpf/
  install -Dm644 "$srcdir"/sysdig-$pkgver/build/driver/bpf/probe.o "$pkgdir"/usr/src/sysdig-${pkgver}/bpf/
  sed -i '/probe\.o/s/\<mv\>/cp --reflink=auto/' "$pkgdir"/usr/bin/scap-driver-loader

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 "$srcdir"/sysdig-$pkgver/{NOTICES,COPYING} "$pkgdir"/usr/share/licenses/$pkgname
}
