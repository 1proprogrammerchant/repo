# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=mediapipe
pkgname=python-mediapipe
pkgver=0.8.10.2
pkgrel=1
pkgdesc='Cross-platform, customizable ML solutions for live and streaming media'
arch=('x86_64')
url='https://mediapipe.dev'
license=('Apache')
depends=(
  absl-py
  ffmpeg
  openexr
  python-attrs
  python-matplotlib
  python-numpy
  python-opencv
  python-protobuf
)
makedepends=(
  bazel
  python-setuptools
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/google/mediapipe/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('a312bf8e69dbe34d3b5c7ea226dcc122aa9e640cbfbf35e1be65d0150b35f1e5')

prepare() {
  cd "${_pkgname}-${pkgver}"
  # remove restriction on bazel version
  rm -vf .bazelversion
  # build with opencv4
  # see also https://google.github.io/mediapipe/getting_started/install.html
  sed -i 's!#"include/opencv4/!"include/opencv4/!g' third_party/opencv_linux.BUILD
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py gen_protos
  python setup.py install --root="${pkgdir}" --optimize=1 --link-opencv
}
# vim:set ts=2 sw=2 et:
