_name=django-formtools
pkgname=python-django-formtools
pkgver=2.3
pkgrel=2
pkgdesc="A set of high-level abstractions for Django forms"
arch=(any)
url="https://django-formtools.readthedocs.io/en/latest/"
license=('BSD')
depends=('python-django' 'python-setuptools-scm')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/django-formtools-2.3.tar.gz")
sha256sums=('9663b6eca64777b68d6d4142efad8597fe9a685924673b25aa8a1dcff4db00c3')

build() {
  cd "$srcdir/django-formtools-2.3"
  python3 setup.py build
}

package() {
  cd "$srcdir/django-formtools-2.3"
  python3 setup.py install --root=$pkgdir --optimize=1 --skip-build

  # make sure we don't install any world-writable or root-readable-only files
  # we shouldn't need to fix ownership as we extract tarballs as a non-root user
  # https://github.com/pypa/setuptools/issues/1328
  # https://github.com/LonamiWebs/Telethon/issues/1605
  chmod u=rwX,go=rX -R "$pkgdir"
  # make sure we don't install annoying files
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "$pkgdir/$_site_packages/tests/"
}

