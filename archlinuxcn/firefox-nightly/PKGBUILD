# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>
# Contributor: Cedric MATHIEU <me.xenom @ gmail.com>

_name=firefox
_channel=nightly
pkgbase=firefox-nightly
pkgname=()
_version=111.0a1
pkgver=111.0a1.20230204.12
pkgrel=1
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('dbus-glib' 'gtk3' 'libxt' 'nss' 'mime-types')
optdepends=('pulseaudio: audio support'
            'ffmpeg: h.264 video'
            'hunspell: spell checking'
            'hyphen: hyphenation'
            'libnotify: notification integration'
            'networkmanager: location detection via available WiFi networks'
            'speech-dispatcher: text-to-speech'
            'xdg-desktop-portal: Screensharing with Wayland'
            'startup-notification: support for FreeDesktop Startup Notification')
_url="https://ftp.mozilla.org/pub/${_name}/nightly/latest-mozilla-central-l10n"
_filename_prefix="20230204.12-"
source=("${pkgbase}.desktop")
sha512sums=('b58c1b3b09772d0866d511e83e3c7f54db0fe71d2978d2a06bd19b25c020f16e201aab825f0b3d5e9f0c1a3e9db028018e5e38f7ee4868e321938f11cb5ac817'
            '0a82c52f279b79ccfd0cc56fbb6be70cab78cdd0a2058e6881c4e464c30cae47b017aa287375f44f26fc6e1fb4462ee809cc46024d7a9e6118623e3769002247'
            'SKIP'
            '3644259815d7b0f86e482064c33cd790f1d7b54b308b657496a3b8b88bb7e7755ece8fcb07cd32f1ce4b88c96db54176e364936ce1393f9f581230cf3303ccc4'
            'SKIP'
            '3fc55c3ce7e52f85cd1c063634722ec64a40644cc9fb2b2172dfb74218750c9dea2cad7e4d5276698a8c14e3c4521e0017b56f4c92647dd57bb4d68d49dfc5d2'
            'SKIP'
            'b542b4a69db5c723b366dd36eb88801552702d37e51a5f52cf848c807b83b2c18df523760d24ca887ca5c60e6846c433ccbc97c36f0605274fab4f4a8ff17928'
            'SKIP')
validpgpkeys=('14F26682D0916CDD81E37B6D61B7B526D98F0353'
              '097B313077AE62A02F84DA4DF1A6668FBB7D572E') # Mozilla’s GnuPG release key

_languages=(en-US zh-CN ja zh-TW)

for _lang in "${_languages[@]}"; do
  _locale=${_lang}
  _pkgname=firefox-nightly-${_locale,,}
  _src="${_name}-${_version}.${_lang}.linux"
  _filename="${_filename_prefix}${_src}-x86_64.tar.bz2"

  pkgname+=($_pkgname)
  source+=("${_filename}"::"${_url}/${_src}-x86_64.tar.bz2"
           "${_filename}.asc"::"${_url}/${_src}-x86_64.tar.bz2.asc")
  eval "package_$_pkgname() {
    msg2 'Removing old firefox directory...'
    rm -rf firefox
    msg2 'Extract ${_filename}...'
    bsdtar -xf ${_filename}
    _package $_lang
  }"
done

# Don't extract anything because they'll override each other
noextract=(${source[@]%%::*})

_package() {
  OPT_PATH="opt/${pkgbase}"
  pkgdesc="Standalone Web Browser from Mozilla — Nightly build ($1)"
  url="https://www.mozilla.org/$1/${_name}/${_channel}"
  provides=(firefox-${_channel}=$pkgver)
  conflicts=(firefox-${_channel} firefox-${_channel}-i18n-${1,,})
  replaces=(firefox-${_channel}-i18n-${1,,})

  # Install the package files
  install -d "${pkgdir}"/{usr/bin,opt}
  cp -r ${_name} "${pkgdir}"/${OPT_PATH}
  ln -s "/${OPT_PATH}/${_name}" "${pkgdir}"/usr/bin/${pkgbase}

  # Install .desktop files
  install -Dm644 "${srcdir}"/${pkgbase}.desktop -t "${pkgdir}"/usr/share/applications

  # Install icons
  SRC_LOC="${srcdir}"/${_name}/browser
  DEST_LOC="${pkgdir}"/usr/share/icons/hicolor
  for i in 16 32 48 64 128; do
      install -Dm644 "${SRC_LOC}"/chrome/icons/default/default${i}.png "${DEST_LOC}"/${i}x${i}/apps/${pkgbase}.png
  done

  cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/browser/defaults/preferences/vendor.js
pref("intl.locale.requested", "");
pref("spellchecker.dictionary_path", "/usr/share/hunspell");
EOF

  # Disable auto-updates
  if [[ $1 == "zh-CN" ]]; then
    # Change homepage for zh_CN to avoid ADs and tracks
    cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/distribution/policies.json
{"policies": {"DisableAppUpdate": true, "Homepage": {"URL": "about:home"}}}
EOF
  else
    cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/distribution/policies.json
{"policies": {"DisableAppUpdate": true}}
EOF
  fi
}
