# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-lily
pkgver=465.27
pkgrel=1
pkgdesc="NVIDIA drivers for linux-lily"
arch=('x86_64')
url="https://www.nvidia.com/"
depends=("linux-lily=5.10.29-1" "nvidia-utils=${pkgver}" 'libglvnd')
makedepends=("nvidia-utils=${pkgver}" 'libglvnd' 'linux-lily' 'linux-lily-headers')
license=('custom')
options=('!strip')
provides=("nvidia=$pkgver")
conflicts=('nvidia-dkms')
_pkg="NVIDIA-Linux-x86_64-${pkgver}"
source=("https://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run")
sha512sums=('1d43f436b1305faa2981de6ebfa0916d10127005b6cb765ab6bf0d1e75529ec1eab973d0de6e3ebb39678d2b3f80f2ac9a661ed0c4690d8a823ac7822135cdb2')

prepare() {
    sh "${_pkg}.run" --extract-only
}

build() {
    cd "${_pkg}"/kernel
    make SYSSRC=/usr/src/linux-lily module
}

package() {

    _extradir="/usr/lib/modules/$(</usr/src/linux-lily/version)/extramodules"
    install -Dt "${pkgdir}${_extradir}" -m644 \
      "${srcdir}/${_pkg}/kernel"/nvidia{,-modeset,-drm,-uvm}.ko

    find "${pkgdir}" -name '*.ko' -exec xz {} +

    echo "blacklist nouveau" |
        install -Dm644 /dev/stdin "${pkgdir}/usr/lib/modprobe.d/${pkgname}.conf"

    install -Dt "${pkgdir}/usr/share/licenses/${pkgname}" -m644 "${srcdir}/${_pkg}/LICENSE"
}
