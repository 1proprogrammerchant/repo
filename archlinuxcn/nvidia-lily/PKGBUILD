# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-lily
pkgver=520.56.06
pkgrel=2
pkgdesc="NVIDIA drivers for linux-lily"
arch=('x86_64')
url="https://www.nvidia.com/"
depends=("linux-lily=5.15.77-1" "nvidia-utils=${pkgver}" 'libglvnd')
makedepends=("nvidia-utils=${pkgver}" 'libglvnd' 'linux-lily' 'linux-lily-headers')
license=('custom')
options=('!strip')
provides=("nvidia=$pkgver")
conflicts=('nvidia-dkms')
_pkg="NVIDIA-Linux-x86_64-${pkgver}"
source=("https://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run")
sha512sums=('ac5a98964e1b7435a0919d313080e53c0453ca3a817ed1ed1693a0164f286973d93f4130428a3737a4871f512c686d72e2606038f533d016d6b9c2873eaa1f8a')

prepare() {
    sh "${_pkg}.run" --extract-only
}

build() {
    cd "${_pkg}"/kernel
    make SYSSRC=/usr/src/linux-lily module
}

package() {

    _extradir="/usr/lib/modules/$(</usr/src/linux-lily/version)/extramodules"
    install -Dt "${pkgdir}${_extradir}" -m644 \
      "${srcdir}/${_pkg}/kernel"/nvidia{,-modeset,-drm,-uvm}.ko

    find "${pkgdir}" -name '*.ko' -exec xz {} +

    echo "blacklist nouveau" |
        install -Dm644 /dev/stdin "${pkgdir}/usr/lib/modprobe.d/${pkgname}.conf"

    install -Dt "${pkgdir}/usr/share/licenses/${pkgname}" -m644 "${srcdir}/${_pkg}/LICENSE"
}
