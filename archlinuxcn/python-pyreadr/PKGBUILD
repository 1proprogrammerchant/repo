# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-pyreadr
_pkgname=pyreadr
pkgver=0.4.4
pkgrel=1
pkgdesc='Read and write R RData and Rds files into/from pandas dataframes'
arch=('x86_64')
url='https://github.com/ofajardo/pyreadr'
license=('AGPL3')
depends=(
  python-pandas
  xz
)
makedepends=(
  cython  
  python-setuptools
)
checkdepends=(
  python-sphinx_rtd_theme
  python-xarray
)
optdepends=(
  "python-xarray: for reading 3D arrays"
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/ofajardo/pyreadr/archive/refs/tags/v${pkgver}.tar.gz"
)
sha512sums=('522d2da66455f1464506cfe636f4d8d3ffb4213a412db0914ff259908ae650219ae6915e746bef4ce72fd204bc780f3243872fed596aaefa0f0a9424aeac7deb')

get_pyver() {
  python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_pkgname}-${pkgver}"
  PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-$(get_pyver)" python tests/test_basic.py
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
# vim:set ts=2 sw=2 et:
