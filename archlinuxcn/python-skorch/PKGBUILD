# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=skorch
pkgname=python-skorch
pkgver=0.11.0
pkgrel=1
pkgdesc='A scikit-learn compatible neural network library that wraps pytorch'
arch=(any)
url=https://github.com/skorch-dev/skorch
license=(BSD)
depends=(
  python-numpy
  python-pandas
  python-pytorch
  python-scikit-learn
  python-scipy
  python-tabulate
  python-tqdm)
makedepends=(python-setuptools)
checkdepends=(flake8 python-flaky python-pytest python-pytest-cov)
source=("${_pkgname}-${pkgver}.tar.gz"::"https://github.com/skorch-dev/skorch/archive/v${pkgver}.tar.gz")
sha512sums=('372050145b948f38f6ea435b98767e47507d6a2c914872f579e078a14feb596cc43e55f5a6012f12e078d4f544bb184e5f41a0228fefb15fb9b787393e653ca6')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_pkgname}-${pkgver}"
  pytest -v
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
