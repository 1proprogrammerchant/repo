# Maintainer: Hu Butui <hot123tea123@gmail.com>

_pkgname=biomaRt
_pkgver=2.48.1
pkgname=r-${_pkgname,,}
pkgver=2.48.1
pkgrel=1
pkgdesc='Interface to BioMart databases (i.e. Ensembl)'
arch=('any')
url="https://bioconductor.org/packages/${_pkgname}"
license=('Artistic2.0')
depends=(
  r
  r-annotationdbi
  r-biocfilecache
  r-digest
  r-httr
  r-progress
  r-rappdirs
  r-stringr
  r-xml
  r-xml2
)
optdepends=(
  r-biocstyle
  r-knitr
  r-mockery
  r-rmarkdown
  r-testthat
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('aae3a938b3fb8334f8e2764da9c1013bd6e4c03ef11644231f38cc1d3e8034a8')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
}
# vim:set ts=2 sw=2 et:
