# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=camelot
pkgname=python-camelot
pkgver=0.10.1
pkgrel=2
pkgdesc='A Python library to extract tabular data from PDFs'
arch=('any')
url='https://github.com/camelot-dev/camelot'
license=('MIT')
depends=(
  ghostscript
  python-chardet
  python-click
  python-pdfminer
  python-matplotlib
  python-numpy
  python-opencv
  python-openpyxl
  python-pandas
  python-pdfminer
  python-pdftopng
  python-pypdf2
  python-tabulate
  tk
)
makedepends=(
  python-setuptools
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/camelot-dev/camelot/archive/v${pkgver}.tar.gz"
  "0001.fix-pypdf-version.patch::https://github.com/camelot-dev/camelot/pull/307.patch"
)
sha512sums=('aeb1446021caccb0aa302d68d8ec17e0598debc29f0521d910c9a14c140a526d14ad8c749bac4fec4e366d8aeced2e9bc36aae1694f172ecbfeca351d97e848d'
            '5bc0665e9fc993f1e15eac02d5471d16b38d3f7288b1efe6c4693daeab007c29f315a0b09bb578ba263060c1c0c92046b6c621e6247eff346c6eb116d870b51d')

prepare() {
  cd "${_pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001.fix-pypdf-version.patch"
  sed -i 's,isEncrypted,is_encrypted,g' camelot/handlers.py
  # python package name is renamed to `pypdf`
  sed -i 's,PyPDF2,pypdf,' camelot/handlers.py
}

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
