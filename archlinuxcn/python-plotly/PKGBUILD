# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Grey Christoforo <first name at last name dot net>

_pkgname=plotly.py
pkgname=python-plotly
pkgver=5.14.1
pkgrel=1
pkgdesc='The interactive graphing library for Python (includes Plotly Express)'
url='https://plotly.com/python'
depends=(
  python-matplotlib
  python-numpy
  python-pandas
  python-six
  python-tenacity
)
optdepends=(
  'ipython: ipython support'
  'python-jupyter: jupyter support'
  'python-ipykernel: jupyter support'
  'python-pillow: image uri conversion'
)
makedepends=(
  jupyter-notebook
  jupyterlab
  npm
  python-build
  python-installer
  python-setuptools
  python-wheel
)
license=('MIT')
arch=('any')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/plotly/plotly.py/archive/v${pkgver}.tar.gz")
sha512sums=('cf053ea47425ad4f4f1412ce8ba037f49609a292b61afaa63bd8c00f010353f56a648cded408b9788055e2bba69e6a71192b55602d11d4122dc76e6dd3085a11')

build() {
  # https://github.com/webpack/webpack/issues/14532
  export NODE_OPTIONS=--openssl-legacy-provider
  cd ${_pkgname}-${pkgver}/packages/python/plotly
  # use_2to3 is deprecated
  sed -i "/use_2to3/d" setup.py
  python -m build --wheel --no-isolation
}

package() {
  cd ${_pkgname}-${pkgver}/packages/python/plotly
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  mv -vf "${pkgdir}/usr/etc" "${pkgdir}"
}
# vim:set ts=2 sw=2 et:
