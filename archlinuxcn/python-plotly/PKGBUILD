# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Grey Christoforo <first name at last name dot net>

_pkgname=plotly.py
pkgname=python-plotly
pkgver=5.4.0
pkgrel=3
pkgdesc='The interactive graphing library for Python (includes Plotly Express)'
url='https://plotly.com/python'
depends=(
  python-matplotlib
  python-numpy
  python-pandas
  python-retrying
  python-six
)
optdepends=(
  'ipython: ipython support'
  'python-jupyter: jupyter support'
  'python-ipykernel: jupyter support'
  'python-pillow: image uri conversion'
)
makedepends=(
  jupyter
  jupyterlab
  npm
  python-setuptools
)
license=('MIT')
arch=('any')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/plotly/plotly.py/archive/v${pkgver}.tar.gz")
sha512sums=('893f6b1f95e2cc59cfd6b12c503ecb7a724e767dab555e234aef0d9871373fb9adc8eb5356eb9ae568aad0a2c20b2827370c5bc965dd790acc33018418260d3e')

build() {
  cd ${_pkgname}-${pkgver}/packages/python/plotly
  python setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}/packages/python/plotly
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  mv -vf "${pkgdir}/usr/etc" "${pkgdir}/usr"
}
# vim:set ts=2 sw=2 et:
