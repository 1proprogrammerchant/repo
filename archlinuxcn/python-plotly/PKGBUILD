# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Grey Christoforo <first name at last name dot net>

_pkgname=plotly.py
pkgname=python-plotly
pkgver=5.2.2
pkgrel=1
pkgdesc='The interactive graphing library for Python (includes Plotly Express)'
url='https://plotly.com/python'
depends=(
  python-matplotlib
  python-numpy
  python-pandas
  python-retrying
  python-six
)
optdepends=(
  'ipython: ipython support'
  'python-jupyter: jupyter support'
  'python-ipykernel: jupyter support'
  'python-pillow: image uri conversion'
)
makedepends=(
  jupyter
  jupyterlab
  npm
  python-setuptools
)
license=('MIT')
arch=('any')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/plotly/plotly.py/archive/v${pkgver}.tar.gz")
sha512sums=('617e95644fb121ee1254e35abd42e38f03a2b3d9407a6160d46f0a5f1d35a87fe26ad29551b56510fc3542d397ff407e3cadde9e8adc3335664079d1b8322412')

build() {
  cd ${_pkgname}-${pkgver}/packages/python/plotly
  python setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}/packages/python/plotly
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
