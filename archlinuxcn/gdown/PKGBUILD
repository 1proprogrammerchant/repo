# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=gdown
pkgname=gdown
pkgver=4.5.2
pkgrel=1
pkgdesc='Download a large file from Google Drive'
arch=('any')
url='https://github.com/wkentaro/gdown'
license=('MIT')
depends=(
  python-beautifulsoup4
  python-filelock
  python-pysocks
  python-requests
  python-six
  python-tqdm
)
makedepends=(
  python-setuptools
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/wkentaro/gdown/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('60ec1a3ceaad7dc5ddd7b31e88d9c582621723c8d85c26338708ca191733f04561ae9ded9224a0137ca8ad959a8fe6bd55c8b9325e0390bad526d60ed35caca6')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
