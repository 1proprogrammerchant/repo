# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=gdown
pkgname=gdown
pkgver=4.4.0
pkgrel=1
pkgdesc='Download a large file from Google Drive'
arch=('any')
url='https://github.com/wkentaro/gdown'
license=('MIT')
depends=(
  python-beautifulsoup4
  python-filelock
  python-pysocks
  python-requests
  python-six
  python-tqdm
)
makedepends=(
  python-setuptools
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/wkentaro/gdown/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('4fcbc88e8f7604c70ae80232cce651eb6ee2bbc3bcf92837cc2de9d52129b3ddb71fd8d9e6194c4c22a72a2e1905a5752f647eb81b353a55bcd3e38f4ab67872')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
