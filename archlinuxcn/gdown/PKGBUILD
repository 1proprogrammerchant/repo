# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=gdown
pkgname=gdown
pkgver=4.3.1
pkgrel=1
pkgdesc='Download a large file from Google Drive'
arch=('any')
url='https://github.com/wkentaro/gdown'
license=('MIT')
depends=(
  python-beautifulsoup4
  python-filelock
  python-pysocks
  python-requests
  python-six
  python-tqdm
)
makedepends=(
  python-setuptools
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/wkentaro/gdown/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('952a54765f1c68643eceb72bb71dae21bf57830d442c849a9f2d768e9c72cfa0d9f4a5fae6a0bda4cae6ee876b6a73153e875c615ee374ae3363fb467a7a0f05')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
