# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=gdown
pkgname=gdown
pkgver=4.6.6
pkgrel=1
pkgdesc='Download a large file from Google Drive'
arch=('any')
url='https://github.com/wkentaro/gdown'
license=('MIT')
depends=(
  python-beautifulsoup4
  python-filelock
  python-pysocks
  python-requests
  python-six
  python-tqdm
)
makedepends=(
  python-setuptools
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/wkentaro/gdown/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('9acd51511ec4467dd3c146064c338e09fb6682b882e6ddf8997f0a2df7916d239128be86783acc73a21ad7f7dbe7bc642abf732c41027ca6ea66ceeca07dff62')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
