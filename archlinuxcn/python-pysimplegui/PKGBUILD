# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: hawkeye116477 <hawkeye116477 at gmail dot com>

_pkgname=PySimpleGUI
pkgname=('python-pysimplegui')
pkgver=4.56.0
pkgrel=1
pkgdesc='Python GUIs for Humans'
arch=('any')
license=('LGPL3')
url='https://github.com/PySimpleGUI/PySimpleGUI'
depends=(
  python
  tk
)
makedepends=(
  python-setuptools
)
source=(${pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('1a13a19282f92626cc6a823cbe9f4aa08aa558870f03441a1c4e8b6cef27c9d5')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
# vim:set ts=2 sw=2 et:
