# Maintainer: Yigit Sever <yigit at yigitsever dot com>
# Contributor: Dominik Schrempf <dominik.schrempf at gmail dot com>
# Contributor: Jonathan Ryan <jryan at curious-computing dot com>
# Contributor: Hussam Al-Tayeb <hussam at visp dot net dot lb>

pkgname=htpdate
pkgver=1.3.4
pkgrel=1
pkgdesc="HTTP based time synchronization tool"
arch=('i686' 'x86_64')
url="https://github.com/twekkel/htpdate"
license=(GPL2)
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "htpdate.default"
  "htpdate.service")
sha256sums=('744f9200cfd3b008a5516c5eb6da727af532255a329126a7b8f49a5623985642'
            'b27efa8fe12de2a2bbacead8e5f926ec26d3010c2a68985f351515939e861864'
            '0c0df47f66e6840b8b5ac70660ec433fa86a24aa396cfaaccba65ed6db8c0237')

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  install -D -m 644 htpdate.default ${pkgdir}/etc/default/htpdate
  install -D -m644 htpdate.service ${pkgdir}/usr/lib/systemd/system/htpdate.service
  cd ${pkgname}-${pkgver}
  install -D -m755 htpdate ${pkgdir}/usr/bin/htpdate
  install -D -m644 htpdate.8 ${pkgdir}/usr/share/man/man8/htpdate.8
}
