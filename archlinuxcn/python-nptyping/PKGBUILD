# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Grey Christoforo <grey at christoforo dot net>

_pkgname=nptyping
pkgname=python-nptyping
pkgver=2.3.1
pkgrel=1
pkgdesc='Type hints for Numpy'
url='https://github.com/ramonhagenaars/nptyping'
arch=('any')
license=('MIT')
depends=(
  python-numpy
)
makedepends=(
  python-setuptools
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/ramonhagenaars/nptyping/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('a80df18cba3d427668be0f44f3ca315cf80daf7e3aed90d76b159479de069054cb4a2442abf7b658ddc853736fc7c3be2b0d1db2d7c9e54938cb1883eb0cb2c9')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
