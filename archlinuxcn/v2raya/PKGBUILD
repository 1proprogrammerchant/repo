# Maintainer: Dct Mei <dctxmei@yandex.com>

pkgname=v2raya
_pkgname=v2rayA
pkgver=1.5.3
pkgrel=1
pkgdesc="A Linux web GUI client of Project V"
arch=('x86_64')
url="https://github.com/v2rayA/v2rayA"
license=('GPL3')
depends=('glibc')
optdepends=('v2ray>=4.37.0-1' 'xray>=1.4.2-1')
makedepends=('go>=2:1.16.0-1' 'nodejs>=14' 'yarn' 'python2')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "v2raya.service"
        "v2raya@.service")
sha256sums=('0f6faeb297279dba138c2afc8525f090f965a0141a4f9d071165ea8500921a55'
            '85002076c39c7de79433321e65ca0c093ace1e837d488bfe4df7fda0567266d7'
            '91f034ae9fbccf5e3155f5a6d6b6dc17147c987dc1f5deb11a0e64a624260571')

build() {
    export OUTPUT_DIR="${srcdir}/${_pkgname}-${pkgver}/service/server/router/web/"
    cd "${srcdir}"/"${_pkgname}-${pkgver}"/gui/
    yarn cache clean
    yarn install
    yarn build
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    cd "$srcdir"/"v2rayA-${pkgver}"/service/
    go build -ldflags="-X=github.com/v2rayA/v2rayA/global.Version=${pkgver} -linkmode=external" -o v2raya
}

package() {
    cd "${srcdir}"/"${_pkgname}-${pkgver}"/
    install -Dm 755 service/v2raya -t "${pkgdir}"/usr/bin/
    install -dm 750 "${pkgdir}"/etc/v2raya/
    install -Dm 644 "${srcdir}"/v2raya.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm 644 "${srcdir}"/v2raya@.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm 644 install/universal/v2raya.desktop -t "${pkgdir}"/usr/share/applications/
    install -Dm 644 gui/public/img/icons/android-chrome-512x512.png "${pkgdir}"/usr/share/icons/hicolor/512x512/apps/v2raya.png
}
