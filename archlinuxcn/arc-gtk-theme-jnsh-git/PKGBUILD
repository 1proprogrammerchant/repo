# Maintainer: Shengyu Zhang <la@archlinuxcn.org>
# Contributor: Joonas Henriksson <joonas.henriksson at gmail com>

pkgname=arc-gtk-theme-jnsh-git
_pkgname=arc-theme
pkgver=r1265.677e82a7
pkgrel=1
pkgdesc="A flat theme suite with transparent elements. Fork maintained by jnsh."
arch=('any')
url="https://github.com/jnsh/arc-theme"
license=('GPL3')
makedepends=('sassc' 'inkscape' 'optipng' 'git' 'meson')
optdepends=('gtk-engine-murrine: GTK2 support'
            'gnome-themes-extra: GTK2 support')
source=("${_pkgname}::git+https://github.com/jnsh/${_pkgname}.git")
md5sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

_prefix='/usr'

build() {
  cd "${_pkgname}"
  meson setup --prefix="${_prefix}" -Dthemes=gnome-shell,gtk2,gtk3,metacity,plank,unity,xfwm builddir
  ninja -C builddir
}

package() {
  conflicts=('arc-gtk-theme' 'arc-gtk-theme-git')

  cd "${_pkgname}"
  DESTDIR="${pkgdir}" ninja -C builddir install
}
