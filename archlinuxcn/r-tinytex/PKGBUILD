# Maintainer: Hu Butui <hot123tea123@gmail.com>

_pkgname=tinytex
_pkgver=0.35
pkgname=r-${_pkgname,,}
pkgver=0.35
pkgrel=1
pkgdesc='Helper Functions to Install and Maintain TeX Live, and Compile LaTeX Documents'
arch=('any')
url="https://cran.r-project.org/package=${_pkgname}"
license=('MIT')
depends=(
  r
  r-xfun
)
optdepends=(
  r-rstudioapi
  r-testit
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('d36397f677d1699337374cbbe415a6cc351023ed01b224c9d751e8e62619cf12')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
  install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
