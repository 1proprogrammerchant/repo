# Maintainer: grufo <madmurphy333 at gmail.com>

pkgname='proot-git'
pkgver='r1216.51b9796'
pkgrel=1
pkgdesc='chroot, mount --bind, and binfmt_misc without privilege/setup'
arch=(armv7h aarch64 i686 x86_64)
url='http://proot.me'
license=('GPL')
provides=('proot')
conflicts=('proot' 'proot-bin')
depends=('talloc')
makedepends=('git' 'python-docutils' 'libxslt')
source=("git://github.com/proot-me/PRoot"
        0001-Fix-regression-in-socket-name-shortening.patch
        0002-Make-sure-not-to-fake-too-old-an-kernel-release.patch
        0003-Python-3-support-in-test.patch
        0004-Allow-a-higher-initial-heap-size-in-test.patch
        0005-Make-sure-the-stack-is-aligned.patch
        0006-Access-sockfd-in-the-chained-getsocketname-via-the-o.patch)
sha1sums=('SKIP'
          '10df01ccb8aadfc0d4814d0f3fd487cfd9b17eb2'
          '86afae92e333466df866eb40ded7936d829fc3e0'
          '3a05a00a82e9ee97b54a5e626a8f886f8c2d5255'
          '742d934557e40090a89680ed661a7de09a574c68'
          'dd15b3d426ec2cd8c8cfbccc91bb3034422e6782'
          'd842877abb98fc2a9bf57a35777433c3c7441e01')

pkgver() {
  cd PRoot
  printf "'r%s.%s'" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd PRoot


  patch -Np1 < ../0001-Fix-regression-in-socket-name-shortening.patch
  patch -Np1 < ../0002-Make-sure-not-to-fake-too-old-an-kernel-release.patch
  patch -Np1 < ../0003-Python-3-support-in-test.patch
  patch -Np1 < ../0004-Allow-a-higher-initial-heap-size-in-test.patch
  patch -Np1 < ../0005-Make-sure-the-stack-is-aligned.patch
  patch -Np1 < ../0006-Access-sockfd-in-the-chained-getsocketname-via-the-o.patch
}

build() {
  make -C PRoot/src -f GNUmakefile

  make -C PRoot/doc -f GNUmakefile
}

package() {
  cd PRoot

  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 "src/proot" "${pkgdir}/usr/bin"

  install -m755 -d "${pkgdir}/usr/share/man/man1/"
  install -m644 -T 'doc/proot/man.1' "${pkgdir}/usr/share/man/man1/proot.1"

  install -m755 -d "${pkgdir}/usr/share/doc/proot/"
  install -m644 'doc/proot/changelog.rst' "${pkgdir}/usr/share/doc/proot"
  install -m644 'doc/proot/index.html' "${pkgdir}/usr/share/doc/proot"
  install -m644 'doc/proot/manual.rst' "${pkgdir}/usr/share/doc/proot"
  install -m644 'doc/proot/roadmap.rst' "${pkgdir}/usr/share/doc/proot"

  install -m755 -d "${pkgdir}/usr/share/doc/proot/stylesheets"
  install -m644 'doc/proot/stylesheets/'* "${pkgdir}/usr/share/doc/proot/stylesheets"
}
