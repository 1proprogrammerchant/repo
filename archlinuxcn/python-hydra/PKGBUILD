# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=hydra
pkgname=python-hydra
pkgver=1.1.2
pkgrel=1
pkgdesc='A framework for elegantly configuring complex applications'
arch=('any')
url='https://hydra.cc'
license=('MIT')
depends=(
  python-omegaconf
)
makedepends=(
  java-environment
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/facebookresearch/hydra/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('d3ecc413bff0739745b0be339f0b5e3916e5be48bba3a0d5239e7aecd1e6a2c8075a7b2297faaa2e600c3de6193b64a8d1ba3979a1351765c8732c4d9bac71ac')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
