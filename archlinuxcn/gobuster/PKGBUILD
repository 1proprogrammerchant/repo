# Maintainer: Hao Long <aur@esd.cc>

pkgname=gobuster
pkgver=3.4.0
pkgrel=1
pkgdesc="Directory/File, DNS and VHost busting tool written in Go"
arch=("x86_64" "i686")
url="https://github.com/OJ/gobuster"
license=('Apache')
provides=('gobuster')
conflicts=('gobuster')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/OJ/gobuster/archive/v${pkgver}.tar.gz")
sha256sums=('6c1d7a3aa9604d90ca818d6fc7a0b09501e419ecd4ab7665566c52fd0981b52d')

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm755 gobuster "$pkgdir/usr/bin/gobuster"
}
