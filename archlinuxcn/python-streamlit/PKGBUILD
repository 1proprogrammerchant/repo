# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=streamlit
pkgname=python-streamlit
pkgver=1.7.0
pkgrel=1
pkgdesc='The fastest way to build data apps in Python'
arch=('any')
url='https://streamlit.io'
license=('Apache')
depends=(
  python-altair
  python-astor
  python-attrs
  python-base58
  python-blinker
  python-cachetools
  python-click
  python-dateutil
  python-gitpython
  python-numpy
  python-packaging
  python-pandas
  python-pillow
  python-protobuf
  python-pyarrow
  python-pydeck
  python-pympler
  python-requests
  python-toml
  python-tornado
  python-tzlocal
  python-validators
  python-watchdog
)
makedepends=(
  python-pipenv
  python-setuptools
)

source=("${_pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
)
sha512sums=('01a60e30c3732e8e5eba1910a4c0c42c78d75b48754d5ddaeaa647b152aafd5edf790e54a3890c0031fd769583690dacb5fae53fd0f9603d0a6157a6af042827')

prepare() {
  # relax click requirement
  sed -i 's#click = ">=7.0, <8.0"#click = "*"#' "${_pkgname}-${pkgver}/Pipfile"
}

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=0 --skip-build
  rm -vf "${pkgdir}/usr/bin/streamlit.cmd"
}
# vim:set ts=2 sw=2 et:
