# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=streamlit
pkgname=python-streamlit
pkgver=1.5.0
pkgrel=1
pkgdesc='The fastest way to build data apps in Python'
arch=('any')
url='https://streamlit.io'
license=('Apache')
depends=(
  python-altair
  python-astor
  python-attrs
  python-base58
  python-blinker
  python-cachetools
  python-click
  python-dateutil
  python-gitpython
  python-numpy
  python-packaging
  python-pandas
  python-pillow
  python-protobuf
  python-pyarrow
  python-pydeck
  python-pympler
  python-requests
  python-toml
  python-tornado
  python-tzlocal
  python-validators
  python-watchdog
)
makedepends=(
  python-pipenv
  python-setuptools
)

source=("${_pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
)
sha512sums=('69a9898c692e35d6c37d53cad5cac8a011d80ae0a889ff3e5bb7714622c5bf1c75f4a7d3df1036c4a3eea7052ee44b91635b0cb99a3addb554844becb96ead08')

prepare() {
  # relax click requirement
  sed -i 's#click = ">=7.0, <8.0"#click = "*"#' "${_pkgname}-${pkgver}/Pipfile"
}

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=0 --skip-build
  rm -vf "${pkgdir}/usr/bin/streamlit.cmd"
}
# vim:set ts=2 sw=2 et:
