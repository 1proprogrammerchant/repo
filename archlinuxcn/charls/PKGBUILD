# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=charls
pkgver=2.3.0
pkgrel=1
pkgdesc='A C++ JPEG-LS library implementation'
arch=('x86_64')
url='https://github.com/team-charls/charls'
license=('BSD')
depends=('gcc-libs')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/team-charls/charls/archive/${pkgver}.tar.gz")
sha512sums=('2c990dfd2f247741efaeaa16e1eea716eed214ca249ba1f39458ad0011109bb1b56813ff0226365b6b3610cbc73f4853541e91583a096034b3cee007fc8f071c')

build() {
  echo "CXXFLAGS: ${CXXFLAGS}"
  echo "CPPFLAGS: ${CPPFLAGS}"
  export CXXFLAGS+=" ${CPPFLAGS}"
  cmake \
    -B "${pkgname}-${pkgver}/build" \
    -S "${pkgname}-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -Wno-dev
  make -C "${pkgname}-${pkgver}/build"
}

package() {
  make -C "${pkgname}-${pkgver}/build" DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/${pkgname}-${pkgver}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
# for compatibility
  ln -s "/usr/include/charls" "${pkgdir}/usr/include/CharLS"
  ln -s "/usr/lib/libcharls.so" "${pkgdir}/usr/lib/libCharLS.so.2"
}
# vim:set ts=2 sw=2 et:

