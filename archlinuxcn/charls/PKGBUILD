# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=charls
pkgver=2.3.4
pkgrel=1
pkgdesc='A C++ JPEG-LS library implementation'
arch=('x86_64')
url='https://github.com/team-charls/charls'
license=('BSD')
depends=('gcc-libs')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/team-charls/charls/archive/${pkgver}.tar.gz")
sha512sums=('3f6e1a054d5f7cfa5309e3d1b280d0dbaaeac7f06b08619cefe15677367631d9f6eb96e54b3e5ec921ac50793a570c39178c3e4b52ce9837b731bbccd6a3d0ad')

build() {
  echo "CXXFLAGS: ${CXXFLAGS}"
  echo "CPPFLAGS: ${CPPFLAGS}"
  export CXXFLAGS+=" ${CPPFLAGS}"
  cmake \
    -B "${pkgname}-${pkgver}/build" \
    -S "${pkgname}-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -Wno-dev
  make -C "${pkgname}-${pkgver}/build"
}

package() {
  make -C "${pkgname}-${pkgver}/build" DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/${pkgname}-${pkgver}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
# for compatibility
  ln -s "/usr/include/charls" "${pkgdir}/usr/include/CharLS"
  ln -s "/usr/lib/libcharls.so" "${pkgdir}/usr/lib/libCharLS.so.2"
}
# vim:set ts=2 sw=2 et:

