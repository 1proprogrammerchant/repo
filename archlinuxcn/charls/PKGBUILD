# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=charls
pkgver=2.3.2
pkgrel=1
pkgdesc='A C++ JPEG-LS library implementation'
arch=('x86_64')
url='https://github.com/team-charls/charls'
license=('BSD')
depends=('gcc-libs')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/team-charls/charls/archive/${pkgver}.tar.gz")
sha512sums=('912ff21f4eebff8a43eb0d2a80470bf6fc7c3a49bd7e0e903f9b2ac507e3efdc4c77c9ffdffad85047d181bd277b69efa51d032f9b1e41de1bfa8b1ed540ae6d')

build() {
  echo "CXXFLAGS: ${CXXFLAGS}"
  echo "CPPFLAGS: ${CPPFLAGS}"
  export CXXFLAGS+=" ${CPPFLAGS}"
  cmake \
    -B "${pkgname}-${pkgver}/build" \
    -S "${pkgname}-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -Wno-dev
  make -C "${pkgname}-${pkgver}/build"
}

package() {
  make -C "${pkgname}-${pkgver}/build" DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/${pkgname}-${pkgver}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
# for compatibility
  ln -s "/usr/include/charls" "${pkgdir}/usr/include/CharLS"
  ln -s "/usr/lib/libcharls.so" "${pkgdir}/usr/lib/libCharLS.so.2"
}
# vim:set ts=2 sw=2 et:

