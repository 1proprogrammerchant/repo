# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor : Anthony Wang <ta180m@pm.me>
# Contributor : Ashwin Vishnu <ashwinvis+arch At protonmail DoT cOm>
# Contributor : Immae <ismael.bouya@normalesup.org>
# Contributor : Martin Wimpress <code@flexion.org>
# Contributor : Jingbei Li <i@jingbei.li>

_pkgname=Miniconda3
_pyver=py310
pkgname=miniconda
pkgver=23.3.1
pkgrel=1
pkgdesc='Mini version of Anaconda Python distribution'
arch=('x86_64')
url='https://conda.io/en/latest/miniconda.html'
license=("custom")
depends=(ca-certificates-utils)
source=("${pkgname}-${pkgver}.sh::https://repo.continuum.io/miniconda/${_pkgname}-${_pyver}_${pkgver}-0-Linux-x86_64.sh"
        "${pkgname}.install")
options=(!strip libtool staticlibs)
sha256sums=('aef279d6baea7f67940f16aad17ebe5f6aac97487c7c03466ff01f4819e5a651'
            '02ed5095b7bd9670f749465d9cac1478deab44ebdf8616848ae4bd9371150abf')
install="${pkgname}.install"


package() {
  prefix="${pkgdir}/opt/${pkgname}"
  LD_PRELOAD="/usr/lib/libfakeroot/libfakeroot.so"
  echo "Packaging ${pkgname} for installation to /opt/${pkgname}"
  bash "${srcdir}/${pkgname}-${pkgver}.sh" -b -p ${prefix} -f
  [ "${BREAK_EARLY}" = 1 ] && exit 1
  cd "${prefix}"
  echo "Correcting permissions"
  chmod a+r -R pkgs
  echo "Stripping \$pkgdir"
  sed "s|${pkgdir}||g" -i $(grep "$pkgdir" . -rIl)
  echo "Installing license"
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # use system /etc/ssl/certs/ca-certificates.crt
  find "${pkgdir}" -type f -name "cacert.pem" -exec ln -sf "/etc/ssl/certs/ca-certificates.crt" {} \;
}
