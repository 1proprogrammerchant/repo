# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor : Anthony Wang <ta180m@pm.me>
# Contributor : Ashwin Vishnu <ashwinvis+arch At protonmail DoT cOm>
# Contributor : Immae <ismael.bouya@normalesup.org>
# Contributor : Martin Wimpress <code@flexion.org>
# Contributor : Jingbei Li <i@jingbei.li>

_pkgname=Miniconda3
_pyver=py39
pkgname=miniconda
pkgver=4.10.3
pkgrel=4
pkgdesc='Mini version of Anaconda Python distribution'
arch=('x86_64')
url='https://conda.io/en/latest/miniconda.html'
license=("custom")
source=("${pkgname}-${pkgver}.sh::https://repo.continuum.io/miniconda/${_pkgname}-${_pyver}_${pkgver}-Linux-x86_64.sh"
        "${pkgname}.install")
options=(!strip libtool staticlibs)
sha256sums=('1ea2f885b4dbc3098662845560bc64271eb17085387a70c2ba3f29fff6f8d52f'
            '02ed5095b7bd9670f749465d9cac1478deab44ebdf8616848ae4bd9371150abf')
install="${pkgname}.install"


package() {
  prefix="${pkgdir}/opt/${pkgname}"
  LD_PRELOAD="/usr/lib/libfakeroot/libfakeroot.so"
  echo "Packaging ${pkgname} for installation to /opt/${pkgname}"
  bash "${srcdir}/${pkgname}-${pkgver}.sh" -b -p ${prefix} -f
  [ "${BREAK_EARLY}" = 1 ] && exit 1
  cd "${prefix}"
  echo "Correcting permissions"
  chmod a+r -R pkgs
  echo "Stripping \$pkgdir"
  sed "s|${pkgdir}||g" -i $(grep "$pkgdir" . -rIl)
  echo "Installing license"
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
