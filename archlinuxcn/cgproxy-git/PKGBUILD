# Maintainer: Fancy Zhang <springzfx@gmail.com>
# Maintainer: SiHuan <sihuan at sakuya dot love>
pkgname=cgproxy-git
pkgver=v0.19.r3.gaaa628a
pkgrel=3
pkgdesc="A transparent proxy program powered by cgroup2 and tproxy"
arch=('x86_64')
url="https://github.com/springzfx/cgproxy"
license=('GPL')
groups=('')
makedepends=('cmake' 'nlohmann-json' 'git')
depends=('libbpf'  'iproute2' 'which')
provides=('cgproxy')
conflicts=('cgproxy')

source=("${pkgname}::git+https://github.com/springzfx/cgproxy#branch=master"
        "fix_libbpf.patch::https://github.com/springzfx/cgproxy/commit/e88dc22803ea2fa7a6b62050178862e8a4ec10ab.patch")
sha256sums=('SKIP'
            '874ca17cded7406eac5243b816adf61cbe03ac204ade10e18b91cf9a00629e85')

pkgver() {
  cd "$pkgname"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd "$pkgname"
  patch -Np1 -i ${srcdir}/fix_libbpf.patch
}

backup=('etc/cgproxy/config.json')
install="cgproxy.install"

build(){
    mkdir -p "${srcdir}/${pkgname}/build"
    cd "${srcdir}/${pkgname}/build"
    cmake -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -Dbuild_execsnoop_dl=ON \
          -Dbuild_static=OFF \
          .. 
    make
}

package_cgproxy-git(){
    cd "${srcdir}/$pkgname"/build
    make DESTDIR=$pkgdir install
}


