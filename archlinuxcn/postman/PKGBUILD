# Maintainer: Bruce Zhang
pkgname=postman
pkgver=7.36.1
pkgrel=1
pkgdesc='Build, test, and document your APIs faster'
arch=('x86_64')
url='https://www.getpostman.com/downloads/'
license=('custom:Postman EULA')
depends=('electron8')
provides=('postman')
conflicts=('postman-bin')
options=('!strip')
source_x86_64=(
  "$pkgname-$pkgver-x86_64.orig.tar.gz::https://dl.pstmn.io/download/version/$pkgver/linux64"
)
source=(
  "postman.desktop"
  "postman.sh"
)
sha1sums=('35194ee810e13d3a21f224c6d46104fc201631c1'
          '28a2c853f1a511fc5f253b4fbc4b18adeddb6ba0')
sha1sums_x86_64=('0f3b1e5bd0cdc1ca6ff8a467fe5cc5d3cb58ce1c')

package() {
  cd "$srcdir/Postman/app/resources/app"

  # Install Postman app resource
  find . -type f -exec install -Dm644 {} "$pkgdir/usr/share/postman/app/{}" \;

  # Install bin
  install -Dm755 "$srcdir/postman.sh" "$pkgdir/usr/bin/postman"

  # Install desktop
  install -Dm755 "$srcdir/postman.desktop" "$pkgdir/usr/share/applications/postman.desktop"

  # Install icons & EULA
  install -Dm644 "$srcdir/Postman/app/resources/app/assets/icon.png" "$pkgdir/usr/share/icons/hicolor/128x128/apps/postman.png"
  mkdir -p "$pkgdir/usr/share/licenses/postman"
  echo "https://www.postman.com/licenses/postman-eula/" > "$pkgdir/usr/share/licenses/postman/EULA.txt"
}
