# Maintainer: nariox <pedro(dot)nariyoshi(at)gmail(dot)com>

pkgname=rtl8821cu-dkms-git
_pkgbase=rtl8821cu
epoch=1
pkgver=5.4.1.122.9828dcb
pkgrel=2
pkgdesc="rtl8821cu chipset driver"
arch=('any')
url="https://github.com/brektrou/rtl8821CU"
license=('GPL2')
depends=('dkms' 'bc') # bc dependency needed for GCC 4.9 detection in makefile
makedepends=('git')
conflicts=("${_pkgbase}")
source=("git+https://github.com/brektrou/rtl8821CU"
        0001-Make-flags.patch
        0002-Get-rid-of-hardware-float.patch
        'dkms.conf')
sha256sums=('SKIP'
            '7f0704ae047350bb2a4a13a190666061bba598bdfd8c00602d4cf854670392fc'
            '6ad8c5e562adfb75c43e9932bf4d1bbf6ed1497f2b1d44a7ba7f779d22f26718'
            'dc259d528f88d10b0026f7ee5815669d9f700485418278292f521e98afa54ba1')
pkgver() {
  cd rtl8821CU
  printf '%s.%s.%s' '5.4.1' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd rtl8821CU

  patch -Np1 < ../0001-Make-flags.patch
  patch -Np1 < ../0002-Get-rid-of-hardware-float.patch
}

package() {
  cd rtl8821CU
  mkdir -p "${pkgdir}/usr/src/${_pkgbase}-${pkgver}"
  cp -pr * "${pkgdir}/usr/src/${_pkgbase}-${pkgver}"
  cp ../dkms.conf "${pkgdir}/usr/src/${_pkgbase}-${pkgver}"
  # Set name and version
  sed -e "s/@_PKGBASE@/${_pkgbase}-dkms/" \
      -e "s/@PKGVER@/${pkgver}/" \
      -i "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf
}
