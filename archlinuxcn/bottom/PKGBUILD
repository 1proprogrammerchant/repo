# Maintainer: Hao Long <imlonghao@archlinuxcn.org>

pkgname=bottom
pkgver=0.5.6
pkgrel=1
pkgdesc="A cross-platform graphical process/system monitor with a customizable interface and a multitude of features"
arch=("x86_64" "i686")
url="https://github.com/ClementTsang/bottom"
license=("MIT")
depends=("gcc-libs")
makedepends=("cargo")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('e6b41796cd54dcf67c8b265a81db8275d8557c6b41a48b9ded0a99ce9f9c0d2c')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked --all-features --target-dir=target
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -Dm755 target/release/btm -t "${pkgdir}/usr/bin"
}
