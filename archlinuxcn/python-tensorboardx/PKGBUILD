# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=tensorboardX
pkgname=python-tensorboardx
pkgver=2.5
pkgrel=1
pkgdesc='Tensorboard for pytorch (and chainer, mxnet, numpy, ...)'
arch=('any')
url=https://github.com/lanpa/tensorboardX
license=(MIT)
depends=(python-numpy python-protobuf python-six)
makedepends=(python-setuptools)
checkdepends=(flake8 python-future python-matplotlib python-pytest python-pytorch python-torchvision)
source=("${_pkgname}-${pkgver}.tar.gz"::"https://github.com/lanpa/tensorboardX/archive/v${pkgver}.tar.gz")
sha512sums=('edebfccc6f8f93675c530ed6d8228d42e27f40a925453808e2335970129ea33383235ac9e230256768ade08c2a6fb086d3c3196e6f691f176889ee36ed303bf6')

prepare() {
  cd "${_pkgname}-${pkgver}"
  _ver=$(protoc --version|cut -d ' ' -f 2)
  # using protobuf from official repo
  sed -i "s/DESIRED_PROTO_VERSION=.*/DESIRED_PROTO_VERSION=${_ver}/" compile.sh
  # fix version
  sed -i "/^sha/s/^/#/" setup.py
  sed -i "/^version_git/c\version_git='${pkgver}'" setup.py
}


build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

#check() {
#  cd "${_pkgname}-${pkgver}"
#  pytest -v
#}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
