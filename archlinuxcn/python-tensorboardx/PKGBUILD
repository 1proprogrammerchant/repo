# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=tensorboardX
pkgname=python-tensorboardx
pkgver=2.2
pkgrel=1
pkgdesc='tensorboard for pytorch (and chainer, mxnet, numpy, ...)'
arch=(any)
url=https://github.com/lanpa/tensorboardX
license=(MIT)
depends=(python-numpy python-protobuf python-six)
makedepends=(python-setuptools)
checkdepends=(flake8 python-future python-matplotlib python-pytest python-pytorch python-torchvision)
source=("${_pkgname}-${pkgver}.tar.gz"::"https://github.com/lanpa/tensorboardX/archive/v${pkgver}.tar.gz")
sha512sums=('1c635bf31d55981fc0d166d8badad5b393cca690d2bdcd020841dfd0d207cdd05cb19b8d3a9adcc636a37be80c49b8e2b1cc83dc81d1718d587837ff1f36720b')

prepare() {
  cd "${_pkgname}-${pkgver}"
  _ver=$(protoc --version|cut -d ' ' -f 2)
  # using protobuf from official repo
  sed -i "s/DESIRED_PROTO_VERSION=.*/DESIRED_PROTO_VERSION=${_ver}/" compile.sh
  # fix version
  sed -i "/^sha/s/^/#/" setup.py
  sed -i "/^version_git/c\version_git='${pkgver}'" setup.py
}


build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

#check() {
#  cd "${_pkgname}-${pkgver}"
#  pytest -v
#}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
