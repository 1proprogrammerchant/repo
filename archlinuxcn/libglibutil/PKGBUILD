# Maintainer: Puqns67 <me@puqns67.icu>
# Contributor: Danct12 <danct12@disroot.org>

# This PKGBUILD is base on the AUR repo libglibutil, by Danct12 <danct12@disroot.org>

pkgname='libglibutil'
pkgver=1.0.70
pkgrel=1
pkgdesc='Library of glib utilities'
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url='https://github.com/sailfishos/libglibutil'
license=('BSD')
depends=('glib2')
source=("${pkgname}-${pkgver}.tar.gz"::"${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('87388bdeee86119fe23883dd42e6798ce9c092691b47275c3c75c6d088aa61e5234f98e0bf4de86128a87977d177be982d63ba3d26c8b9d28f3f4b0f5900ed54')

build() {
  make -C "${srcdir}/${pkgname}-${pkgver}/" KEEP_SYMBOLS=1 release pkgconfig
}

package() {
  make -C "${srcdir}/${pkgname}-${pkgver}/" DESTDIR="${pkgdir}" install-dev

  # license
  install -Dm 644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
