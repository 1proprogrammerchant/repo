# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-pydeprecate
_pkgname=pyDeprecate
pkgver=0.3.0
pkgrel=1
pkgdesc="Simple tooling for marking deprecated functions or classes and re-routing to the new successors' instance."
arch=('any')
url='https://github.com/Borda/pyDeprecate'
license=('BSD')
depends=(
  python
)
makedepends=(
  python-setuptools
)
checkdepends=(
  python-pytest
  python-scikit-learn
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/Borda/pyDeprecate/archive/refs/tags/v${pkgver}.tar.gz"
)
sha512sums=('724b9809b235a4b2d143171f029fc524466745989d65c62f3a51699727f62c62717d756cd04a8d4675d90478b1a46b06f7543a5178c47bbbc51e3de38a01c908')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_pkgname}-${pkgver}"
  pytest -v

}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
