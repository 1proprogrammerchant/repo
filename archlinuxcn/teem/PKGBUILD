# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Martino Pilia <martino.pilia@gmail.com>

pkgname=teem
pkgver=1.11.0
pkgrel=8
pkgdesc='A coordinated group of libraries for representing, processing, and visualizing scientific raster data'
arch=('x86_64')
url='http://teem.sourceforge.net/index.html'
license=('LGPL')
depends=(
  bzip2
  fftw
  libpng
)
makedepends=(
  cmake
)
source=("${pkgname}-${pkgver}.tar.gz::https://sourceforge.net/projects/teem/files/teem/${pkgver}/teem-${pkgver}-src.tar.gz"
        "0001.fix-cmake-file-path.patch"
)
sha512sums=('48b171a12db0f02dcfdaa87aa84464c651d661fa66201dc966b3cd5a8134c5bad1dad8987ffcc5d7c21c5d14c2eb617d48200410a1bda19008ef743c093ed575'
            '659d910a58264114f8808cf39e3113c6d368ca74a40940d73cb453107eeddcc78a9800e6b6d948e8f82b72e9353428b70709a275daa991fd6346b42b05747e0c')

prepare() {
  cd "${pkgname}-${pkgver}-src"
  patch -Np1 -i "${srcdir}/0001.fix-cmake-file-path.patch"
}

build() {
  cmake -S "${srcdir}/${pkgname}-${pkgver}-src" \
    -B "build" \
    -DBUILD_EXPERIMENTAL_APPS=ON \
    -DBUILD_EXPERIMENTAL_LIBS=ON \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_BUILD_TYPE:STRING=Release \
    -DCMAKE_INSTALL_PREFIX:PATH="/usr" \
    -DCMAKE_SKIP_INSTALL_RPATH=ON \
    -DTeem_BZIP2=ON \
    -DTeem_FFTW3=ON \
    -DTeem_PNG=ON \
    -DTeem_USE_LIB_INSTALL_SUBDIR=OFF
  make -C "build"
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}-src/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
