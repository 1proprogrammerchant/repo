# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=scenedetect
pkgname=python-scenedetect
pkgver=0.6.1
pkgrel=2
pkgdesc='Python and OpenCV-based scene cut/transition detection program & library'
arch=('any')
url='http://scenedetect.com'
license=('BSD')
depends=(
  python-click
  python-numpy
  python-opencv
  python-tqdm
)
makedepends=(
  python-build
  python-installer
  python-wheel
  python-setuptools
)
optdepends=(
  "ffmpeg: required to split video files"
  "mkvtoolnix-cli: required to split video files in stream copy mode"
)
source=("${_pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('745f3ad57ed788ecc737122e880343c6efb33640285431a43a0bf682eb56eeb6e69e544ab28f5b350b175184ab16c6f9da4fe0d12812b83aa4ebfbeb0836ebb3')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
