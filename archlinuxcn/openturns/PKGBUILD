# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=openturns
pkgver=1.21
pkgrel=1
pkgdesc='Uncertainty treatment library'
license=('LGPL')
arch=('x86_64')
url='http://www.openturns.org'
depends=(
  bison
  blas
  boost
  eigen
  flex
  hdf5
  intel-tbb
  lapack
  libmpc
  libxml2
  muparser
  nlopt
  python-matplotlib
  python-pandas
  python-psutil
  python-scipy
  spectra
)
makedepends=(
  cmake
  swig
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/openturns/openturns/archive/v${pkgver}.tar.gz")
sha256sums=('f91f9b37c738c99761b3fd9eb2d50b2c1e3a8e4466f0f842fdc2445128cb5bda')

build() {
  cmake -B 'build' -S "${srcdir}/${pkgname}-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOPENTURNS_SYSCONFIG_PATH=/etc \
    -DCMAKE_SKIP_INSTALL_RPATH=ON
  make -C 'build'
}

package() {
  make DESTDIR="${pkgdir}" -C 'build' install
  python -O -m compileall "${pkgdir}/usr/lib/"
}
# vim:set ts=2 sw=2 et:

