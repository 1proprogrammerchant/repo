# Maintainer: Y7n05h <Y7n05h(aT)protonmail--d0t--com>
# Contributor:  Emanuel Schmidt <emanuel.schmidt@gmail.com>

pkgname=xdp-tools
pkgver=1.2.3
pkgrel=3
pkgdesc='Utilities for use with XDP and libxdp'
url='https://github.com/xdp-project/xdp-tools'
license=('GPL' 'LGPL' 'BSD')
provides=('libxdp' 'xdp-filter' 'xdp-loader' 'xdpdump' 'libxdp.so')
depends=('libelf' 'linux-api-headers' 'libbpf' 'libpcap.so')
makedepends=('clang' 'llvm' 'gcc' 'make' 'm4' 'pkgconfig' 'libpcap' 'zlib')
arch=('x86_64')

source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('65d54d4bd873ec7850fea8ff8f94f15fc20786e09bee1ff242515e4e7a023952')

build() {
	cd "$pkgname-$pkgver"
	export PRODUCTION=1
	export DYNAMIC_LIBXDP=1
	export FORCE_SYSTEM_LIBBPF=1
	export PREFIX="/usr"
	./configure ## `--prefix` doesn't work yet (https://github.com/xdp-project/xdp-tools/issues/133)
	make V=1
}

package() {
	cd "$pkgname-$pkgver"
	export DESTDIR="${pkgdir}"
	export SBINDIR="/usr/bin"
	export PREFIX="/usr"
	make install V=1
	cd "${pkgdir}/usr"
	rm -r share/xdp-tools ## Do not package examples or tests (https://github.com/xdp-project/xdp-tools/issues/134)
}
