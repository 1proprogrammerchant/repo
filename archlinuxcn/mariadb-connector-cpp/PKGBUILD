# Maintainer: Y7n05h <Y7n05h(aT)protonmail--d0t--com>
pkgname=mariadb-connector-cpp
pkgver=1.1.2
pkgrel=1
pkgdesc=" MariaDB Connector for C++ .(develop branch)"
arch=('x86_64')
url="https://github.com/mariadb-corporation/mariadb-connector-cpp"
license=("LGPL2.1")
conflicts=('mariadb-connector-cpp-git')
depends=('mariadb-libs')
makedepends=('cmake')
provides=('mariadb-connector-cpp')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('8e1226b9c196456124bac505ee688495370cf72e338ecacc16572ae68f62dea5e9f5f8738c04a77d7988b735b180b594fd8ba26c1b0a9b0520a066344dbeacbd')
prepare() {
    cd "$pkgname-$pkgver"
    sed -i "1i\INCLUDE_DIRECTORIES(\"/usr/include/mysql\")" ./CMakeLists.txt
    rm -rf build
    mkdir build
}

build() {
    cd "$pkgname-$pkgver"
    rm "test/CMakeLists.txt"
    rmdir "libmariadb"
    cd "build"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DINSTALL_LIB_SUFFIX=/usr/lib \
        -DUSE_SYSTEM_INSTALLED_LIB=TRUE \
        ..
    make
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/lib/mariadb/* "${pkgdir}"/usr/lib/
  rmdir "${pkgdir}"/usr/lib/mariadb/
}

