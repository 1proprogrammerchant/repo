# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: peippo <christoph.fink@gmail.com>

pkgname=python-av
_pkgname=PyAV
pkgdesc="Pythonic bindings for FFmpeg's libraries"
url='https://pyav.org'
pkgver=9.1.0
pkgrel=1
arch=('x86_64')
license=('BSD')
makedepends=(
  cython
  python-build
  python-installer
  python-setuptools
  python-wheel
)
depends=(
  ffmpeg4.4
  python-numpy
  python-pillow
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/PyAV-Org/PyAV/archive/v${pkgver}.tar.gz")
sha512sums=('1a14894e8100eef1d8123e677ef8d9fa72fe7cb739b21d1168e19e1685d5a2549c98dc431ecefe49e22040dac80b57b3b0a346684fed3040744833791daba13a')

build() {
  cd "${_pkgname}-${pkgver}"
  # https://github.com/PyAV-Org/PyAV/issues/817
  PKG_CONFIG_PATH=/usr/lib/ffmpeg4.4/pkgconfig \
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:

