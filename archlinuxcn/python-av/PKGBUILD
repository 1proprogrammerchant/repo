# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: peippo <christoph.fink@gmail.com>

pkgname=python-av
_pkgname=PyAV
pkgdesc="Pythonic bindings for FFmpeg's libraries"
url='https://pyav.org'
pkgver=9.1.1
pkgrel=1
arch=('x86_64')
license=('BSD')
makedepends=(
  cython
  python-build
  python-installer
  python-setuptools
  python-wheel
)
depends=(
  ffmpeg4.4
  python-numpy
  python-pillow
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/PyAV-Org/PyAV/archive/v${pkgver}.tar.gz")
sha512sums=('7a46c0c85a645b79933102ccccdf775c3a245ed0af520af41505a59b0330317a0d7742c56e39c5815a0f801c12cbc8fde77182a6808578cf7ee4d10bb3a50a77')

build() {
  cd "${_pkgname}-${pkgver}"
  # https://github.com/PyAV-Org/PyAV/issues/817
  PKG_CONFIG_PATH=/usr/lib/ffmpeg4.4/pkgconfig \
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:

