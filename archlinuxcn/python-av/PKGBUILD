# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: peippo <christoph.fink@gmail.com>

pkgname=python-av
_pkgname=PyAV
pkgdesc="Pythonic bindings for FFmpeg's libraries"
url='https://pyav.org'
pkgver=9.0.2
pkgrel=1
arch=('x86_64')
license=('BSD')
makedepends=(
  cython
  python-setuptools
)
depends=(
  ffmpeg4.4
  python-numpy
  python-pillow
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/PyAV-Org/PyAV/archive/v${pkgver}.tar.gz")
sha512sums=('a5373d4f3bf271d11236348f7e4ff806905f29ed51b34c8cda006a79701eee18ccb7e3c0e4342e7e232005f6c58ed8977a9c4cde5381e569cc8bdd5588831a03')

build() {
  cd "${_pkgname}-${pkgver}"
  # https://github.com/PyAV-Org/PyAV/issues/817
  PKG_CONFIG_PATH=/usr/lib/ffmpeg4.4/pkgconfig \
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  PKG_CONFIG_PATH=/usr/lib/ffmpeg4.4/pkgconfig \
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:

