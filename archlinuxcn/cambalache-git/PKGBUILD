# Maintainer: Princeton Ferro (prince781) <princetonferro[at]gmail[dot]com>

pkgname=cambalache-git
_pkgname=cambalache
pkgver=0.7.4.r117.g9381bac
pkgrel=1
pkgdesc='A new RAD tool for Gtk 4 and 3 with a clear MVC design and data model first philosophy'
url=https://gitlab.gnome.org/jpu/cambalache
arch=(any)
license=(LGPL)
depends+=('python-gobject' 'glib2' 'gtk3' 'gtk4' 'python-lxml' 'webkit2gtk')
makedepends+=(git 'meson>=0.50.0')
conflicts=($_pkgname)
provides=($_pkgname=$pkgver)
source=('git+https://gitlab.gnome.org/jpu/cambalache.git' 'fix-build.patch')
sha512sums=('SKIP'
            '6d3030bc1726692dc5d65727b694e0a916b6eeca6702dc1cc33cda429b1b0a37d47c4794bc4cf5f92bfd73970efacc4d27b18825304b06e1d60da8c89e2c3e0d')

prepare() {
    cd "$_pkgname"
    patch --forward --strip=1 --input="${srcdir}/fix-build.patch"
}

pkgver() {
    cd "$_pkgname"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson cambalache build

    ninja -C build
}

package_cambalache-git() {
    DESTDIR="$pkgdir" ninja -C build install
}
