# Maintainer: Songmin Li <lisongmin@protonmail.com>
pkgname=rootlesskit
pkgver=0.14.3
pkgrel=1
pkgdesc='A Linux-native implementation of "fake root" using user_namespaces.'
depends=('glibc')
makedepends=('go')
arch=('i686' 'x86_64' 'aarch64')
url='https://github.com/rootless-containers/rootlesskit'
license=('Apache')
provides=('rootlesskit')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rootless-containers/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('8532693228a39525ab1fd5c96de55a15df219b2e020f64d0ceb8239e8b08106c')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  export GO111MODULE=on

  cd $pkgname-${pkgver}

  make all
}

package() {
  # Install binary
  export DESTDIR=$pkgdir
  export BINDIR=/usr/bin

  cd $srcdir/${pkgname}-${pkgver}
  make install
}
