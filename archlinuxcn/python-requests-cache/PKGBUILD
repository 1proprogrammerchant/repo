# Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>
# Contributor: Ariel AxionL <i [at] axionl [dot] me>

_pyname=requests-cache
pkgname=python-requests-cache
pkgver=0.9.3
pkgrel=1
pkgdesc="Persistent cache for requests library"
arch=('any')
url="https://github.com/reclosedev/requests-cache"
license=('BSD')
depends=('python' 'python-url-normalize')
makedepends=('python-setuptools')
# checkdepends=('python-pytest' 'python-requests-mock' 'python-requests' 'python-pytest-httpbin')

source=("${_pyname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/r/${_pyname}/${_pyname}-${pkgver}.tar.gz")

sha512sums=('7a940ea96985a8cd1c28230106a4bbe0913715398766a2cab1ce3b6be924266c3210e0d992ce3111a895e0a3f28e9d4a23529d770a729556c31c28192fe89be2')

build() {
    cd ${_pyname}-${pkgver}
    python setup.py build
}

# Comment out the unit test temporarily, missing check dependences.
# check() {
#    cd ${_pyname}-${pkgver}/tests
#    USE_PYTEST_HTTPBIN=true pytest ./integration/test_cache.py
# }

package() {
    cd "${srcdir}/${_pyname}-${pkgver}"
    python setup.py install -O1 --root="${pkgdir}" --skip-build
    install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
}
# vim:set et sw=2 ts=2 tw=79:
