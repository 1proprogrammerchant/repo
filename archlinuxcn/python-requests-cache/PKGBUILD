# Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>
# Contributor: Ariel AxionL <i [at] axionl [dot] me>

_pyname=requests-cache
pkgname=python-requests-cache
pkgver=0.9.0
pkgrel=1
pkgdesc="Persistent cache for requests library"
arch=('any')
url="https://github.com/reclosedev/requests-cache"
license=('BSD')
depends=('python' 'python-url-normalize')
makedepends=('python-setuptools')
# checkdepends=('python-pytest' 'python-requests-mock' 'python-requests' 'python-pytest-httpbin')

source=("${_pyname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/r/${_pyname}/${_pyname}-${pkgver}.tar.gz")

sha512sums=('99d29037dc67a1a5a1b7240df198b640132a13ff8a6079eb4471edb513ec28c73f35f784c7cdca4b393faac5a05434504d24f66137ce1607963ab911541d09c2')

build() {
    cd ${_pyname}-${pkgver}
    python setup.py build
}

# Comment out the unit test temporarily, missing check dependences.
# check() {
#    cd ${_pyname}-${pkgver}/tests
#    USE_PYTEST_HTTPBIN=true pytest ./integration/test_cache.py
# }

package() {
    cd "${srcdir}/${_pyname}-${pkgver}"
    python setup.py install -O1 --root="${pkgdir}" --skip-build
    install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
}
# vim:set et sw=2 ts=2 tw=79:
