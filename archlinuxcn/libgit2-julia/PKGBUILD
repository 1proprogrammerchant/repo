# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=libgit2-julia
pkgver=1.2.0
pkgrel=1
epoch=1
pkgdesc='A linkable library for Git'
arch=(i686 x86_64 armv7h aarch64)
url="https://libgit2.github.com/"
depends=('glibc' 'http-parser' 'openssl' 'pcre' 'zlib')
makedepends=('cmake' 'libssh2' 'python')
provides=('libgit2.so' "libgit2=$epoch:$pkgver")
conflicts=(libgit2)
license=('GPL2')
source=("libgit2-$pkgver.tar.gz::https://github.com/libgit2/libgit2/archive/v${pkgver}.tar.gz"
        "libgit2-1.2.0-remove_http-parse_incompatible_tests.patch"
        libgit2-agent-nonfatal.patch)
sha256sums=('41a6d5d740fd608674c7db8685685f45535323e73e784062cf000a633d420d1e'
            '0a597176a551dfe8dda56a46362ff1ef614928206a162d78fdf2d3074c9273ca'
            'a60e169812b802e3fcb4ee7501786346eb87db9ffa3c8ae3311de47ce5a15f10')
options=(!strip)

prepare() {
  cd "libgit2-$pkgver"
  mkdir -p build
  # remove tests that are only compatible with the (modified) vendored version
  # of http-parser, but not with upstream http-parser
  patch -Np1 -i "../libgit2-1.2.0-remove_http-parse_incompatible_tests.patch"
  patch -p1 < ../libgit2-agent-nonfatal.patch
}


build() {
  cd "libgit2-$pkgver"
  export LANG=en_US.UTF-8
  CFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  CXXFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  export CFLAGS+=' -g'
  export CXXFLAGS+=' -g'
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DUSE_HTTP_PARSER=system \
        -DTHREADSAFE=ON \
        -Wno-dev \
        -B build \
        -S .
  make -C build
}

package() {
  depends+=('libssh2.so')
  cd "libgit2-$pkgver"
  make -C build DESTDIR="$pkgdir" install
  install -vDm 644 {AUTHORS,README.md} \
    -t "${pkgdir}/usr/share/doc/libgit2"
}
