# Maintainer: Y7n05h <Y7n05h(aT)protonmail--d0t--com>

pkgbase=activitywatch
# pkgname=('aw-core' 'aw-qt' 'aw-client' 'aw-server-rust' 'aw-server' 'aw-watcher-afk' 'aw-watcher-window')
pkgname=('aw-core' 'aw-qt' 'aw-client' 'aw-server-rust' 'aw-watcher-afk' 'aw-watcher-window')
pkgver='0.11.0'
pkgrel=2
pkgdesc="Log what you do on your computer. Simple, extensible, no third parties."
arch=('any')
url="https://github.com/ActivityWatch/activitywatch"
license=('MPL2')
makedepends=(
    cargo-nightly
    git
    npm
    python-poetry
    python-setuptools
    python-pyqt5
    rust-nightly

)
conflicts=(
    activitywatch-bin
)

source=("${pkgbase}::git+https://github.com/ActivityWatch/activitywatch.git#tag=v${pkgver}")
sha256sums=('SKIP')

prepare() {
    cd "${srcdir}/${pkgbase}"
    git submodule update --init --recursive
}
build() {
    cd "${srcdir}/${pkgbase}/aw-server-rust"
    cargo build --release --bin aw-server
    cd aw-webui
    npm install
    npm run build
}
_poetry_package() {
    local name=$1
    cd "${srcdir}/${pkgbase}/${name}"
    poetry build -f sdist
    tar xvf ./dist/$name-*
    cd $name-*
    python setup.py install --root=${pkgdir}/ --optimize=1
}
package_aw-core() {
    pkgdesc="Core library for ActivityWatch"
    url="https://github.com/ActivityWatch/${pkgname}"
    depends=(
        python-jsonschema
        python-peewee
        python-appdirs
        python-iso8601
        python-takethetime
        python-strict-rfc3339
        python-tomlkit
        python-deprecation
        python-timeslot
        python-json-logger
        python-rfc3339-validator
    )
    optdepends=(python-pymongo)
    provides=($pkgname)
    conflicts=("activitywatch-bin" "${pkgname}-git")

    _poetry_package $pkgname
}
package_aw-client() {
    pkgdesc="Client library for ActivityWatch"
    url="https://github.com/ActivityWatch/${pkgname}"
    depends=(
        aw-core
        python-requests
        python-persist-queue
        python-click
        python-tabulate
        python-typing-extensions
    )
    provides=($pkgname)
    conflicts=("activitywatch-bin" "${pkgname}-git")

    _poetry_package $pkgname
}
package_aw-qt() {
    pkgdesc="Tray icon that manages ActivityWatch processes, built with Qt."
    url="https://github.com/ActivityWatch/${pkgname}"
    depends=(
        aw-core
        aw-server
        python-pyqt5
        python-click
    )
    provides=($pkgname)
    conflicts=("activitywatch-bin" "${pkgname}-git")

    cd "${srcdir}/${pkgbase}/${pkgname}"
    install -Dm644 resources/${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
    install -Dm644 media/logo/logo.png "${pkgdir}/usr/share/icons/hicolor/512x512/apps/activitywatch.png"
    install -Dm644 media/logo/logo-128.png "${pkgdir}/usr/share/icons/hicolor/128x128/apps/activitywatch.png"

    pyrcc5 -o aw_qt/resources.py aw_qt/resources.qrc

    _poetry_package $pkgname

}
package_aw-watcher-afk() {
    pkgdesc="Watches keyboard and mouse activity to determine if you are AFK or not (for use with ActivityWatch)"
    url="https://github.com/ActivityWatch/${pkgname}"
    depends=(
        aw-client
        python-xlib
        python-pynput
    )
    provides=($pkgname)
    conflicts=("activitywatch-bin" "${pkgname}-git")

    _poetry_package $pkgname
}
package_aw-watcher-window() {
    pkgdesc="Cross-platform window watcher (for use with ActivityWatch)"
    url="https://github.com/ActivityWatch/${pkgname}"
    depends=(
        aw-client
        python-xlib
    )
    provides=($pkgname)
    conflicts=("activitywatch-bin" "${pkgname}-git")

    _poetry_package $pkgname
}
package_aw-server-rust() {
    pkgdesc="High-performance implementation of the ActivityWatch server, written in Rust"
    url="https://github.com/ActivityWatch/${pkgname}"
    arch=('x86_64')
    depends=(
        gcc-libs
    )
    provides=(aw-server aw-webui)
    conflicts=("activitywatch-bin" "${pkgname}" "${pkgname}-git" aw-server aw-server-git aw-webui aw-webui-git)

    cd "${srcdir}/${pkgbase}/${pkgname}"

    install -dm0755 "$pkgdir/opt/aw-server"
    install -dm0755 "$pkgdir/usr/bin"

    install -Dm0755 ./target/release/aw-server -t "${pkgdir}/opt/aw-server"
    cp -r ./aw-webui/dist "${pkgdir}/opt/aw-server/static"
    ln -s "/opt/aw-server/aw-server" "${pkgdir}/usr/bin/aw-server"

    install -Dm 644 aw-server.service -t "${pkgdir}"/usr/lib/systemd/user/
}
