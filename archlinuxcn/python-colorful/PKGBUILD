# Maintainer: Hu Butui <hot123tea123@gmail.com>

_name=colorful
pkgname=python-colorful
pkgver=0.5.4
pkgrel=4
pkgdesc='Terminal string styling done right, in Python'
arch=('any')
url='https://github.com/timofurrer/colorful'
license=('MIT')
depends=('python')
makedepends=(
  python-setuptools
)
source=("${_name}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('86848ad4e2eda60cd2519d8698945d22f6f6551e23e95f3f14dfbb60997807ea')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
