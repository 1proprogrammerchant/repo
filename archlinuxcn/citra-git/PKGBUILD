# Maintainer: Tim Schumacher <timschumi@gmx.de>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>
# Contributor: ceri

_pkgbase='citra'
pkgname="$_pkgbase-git"
pkgver=r9544.34de77d42
pkgrel=1
pkgdesc="An experimental open-source Nintendo 3DS emulator/debugger (Master branch)"
arch=('i686' 'x86_64')
url="https://github.com/citra-emu/citra/"
license=('GPL2')
depends=('boost-libs' 'shared-mime-info' 'ffmpeg' 'libusb' 'sdl2' 'qt5-multimedia' 'libxkbcommon-x11' 'cubeb' 'enet' 'libinih' 'discord-rpc' 'fmt' 'dynarmic' 'crypto++')
makedepends=('git' 'rapidjson' 'cmake' 'python' 'clang' 'llvm' 'boost' 'qt5-tools' 'xbyak' 'cpp-jwt' 'nlohmann-json' 'cpp-httplib' 'qt5-wayland' 'desktop-file-utils')
provides=('citra')
conflicts=('citra')
source=("$_pkgbase::git+https://github.com/citra-emu/citra"
ext-lib.patch)
md5sums=('SKIP'
         '91cd68b90e9f7410415dbd857ec5debc')

pkgver() {
	cd "$srcdir/$_pkgbase"
	echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_pkgbase"
    patch -p1 -i ../ext-lib.patch
	mkdir -p build

    git submodule update --init --remote externals/lodepng
    git submodule update --init --remote externals/nihstro
    git submodule update --init --remote externals/soundtouch
    git submodule update --init --remote externals/teakra
    git submodule update --init --remote externals/dds-ktx
}

build() {
	# Trick the compiler into thinking we're building from a continuous
	# integration tool so the build number is correctly shown in the title
	cd "$srcdir/$_pkgbase"

    CXXFLAGS+=" -I/usr/include/libusb-1.0"
	mkdir -p build && cd build
	cmake .. \
	  -DCMAKE_INSTALL_PREFIX=/usr \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DENABLE_QT_TRANSLATION=ON \
      -DCITRA_ENABLE_COMPATIBILITY_REPORTING=ON \
      -DENABLE_COMPATIBILITY_LIST_DOWNLOAD=ON \
      -DUSE_DISCORD_PRESENCE=ON \
      -DENABLE_SCRIPTING=ON \
      -DENABLE_FFMPEG_VIDEO_DUMPER=ON \
      -DENABLE_FFMPEG_AUDIO_DECODER=ON \
      -DUSE_SYSTEM_BOOST=ON \
      -DUSE_SYSTEM_SDL2=ON \
      -DENABLE_MF=OFF \
      -DCITRA_TESTS=OFF \
      -DCITRA_CHECK_SUBMODULES=OFF

    sed -i 's/ -lxbyak//g' src/citra_qt/CMakeFiles/citra-qt.dir/link.txt
    sed -i 's/ -lxbyak//g' src/citra/CMakeFiles/citra.dir/link.txt
	make
}

package() {
	cd "$srcdir/$_pkgbase/build"
	DESTDIR="$pkgdir/" make install
}
