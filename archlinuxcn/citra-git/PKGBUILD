# Maintainer: Tim Schumacher <timschumi@gmx.de>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>
# Contributor: ceri

_pkgbase='citra'
pkgname="$_pkgbase-git"
pkgver=r9157.54a36bc54
pkgrel=1
pkgdesc="An experimental open-source Nintendo 3DS emulator/debugger (Master branch)"
arch=('i686' 'x86_64')
url="https://github.com/citra-emu/citra/"
license=('GPL2')
depends=('boost-libs' 'shared-mime-info' 'desktop-file-utils' 'ffmpeg' 'libfdk-aac' 'libusb' 'sdl2' 'qt5-base' 'qt5-multimedia' 'libxkbcommon-x11')
makedepends=('git' 'rapidjson' 'cmake' 'python' 'clang' 'llvm' 'boost' 'qt5-tools')
optdepends=('qt5-wayland: for Wayland support')
provides=('citra')
conflicts=('citra')
source=("$_pkgbase::git+https://github.com/citra-emu/citra" "ffmpeg.patch::https://github.com/citra-emu/citra/commit/04f201ab348f24772156f0e69aee319fc0242700.patch")
md5sums=('SKIP'
         '433b9c1a70e6d6dcb10123fe4493fc8d')
pkgver() {
	cd "$srcdir/$_pkgbase"
	echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_pkgbase"
	mkdir -p build

	git submodule init
	git submodule update --init --recursive
    patch -p1 -i ../ffmpeg.patch
}

build() {
	# Trick the compiler into thinking we're building from a continuous
	# integration tool so the build number is correctly shown in the title
	cd "$srcdir/$_pkgbase"
	CXXFLAGS+=" -DFMT_USE_USER_DEFINED_LITERALS=0"
	
	mkdir -p build && cd build
	cmake .. \
	  -DCMAKE_INSTALL_PREFIX=/usr \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DENABLE_QT_TRANSLATION=ON \
	  -DCITRA_ENABLE_COMPATIBILITY_REPORTING=ON \
	  -DENABLE_COMPATIBILITY_LIST_DOWNLOAD=ON \
	  -DUSE_DISCORD_PRESENCE=ON \
    -DUSE_SYSTEM_BOOST=ON \
	  -DENABLE_FFMPEG_VIDEO_DUMPER=ON \
    -DENABLE_MF=OFF \
    -DENABLE_FDK=ON
	make
}

check() {
	cd "$srcdir/$_pkgbase/build"
	make test
}

package() {
	cd "$srcdir/$_pkgbase/build"
	DESTDIR="$pkgdir/" make install
}
