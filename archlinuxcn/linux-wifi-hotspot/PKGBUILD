# Maintainer: Dct Mei <dctxmei@yandex.com>
# Maintainer: Mark Wagie <mark dot wagie at tutanota dot com>
# Contributor: Dimitris Kiziridis <ragouel at outlook dot com>

pkgname=linux-wifi-hotspot
pkgver=4.0.3
pkgrel=2
pkgdesc="Create virtual wifi hotspot using same wifi card which is connected to an AP + many features (a GUI tool)"
arch=('x86_64')
url="https://github.com/lakinduakash/linux-wifi-hotspot"
license=('BSD')
depends=('hostapd' 'iw' 'gtk3' 'procps-ng' 'dnsmasq' 'iproute2')
optdepends=('haveged: For random MAC generation'
            'wireless_tools: if iw cannot recognize your adapter'
            'bash-completion: for bash completions')
provides=('wihotspot' 'create_ap')
conflicts=('wihotspot' 'create_ap')
backup=('etc/create_ap.conf')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "org.opensuse.policykit.wihotspot.rules")
sha256sums=('39298a46daa41ac91034564f2e0a64d4afb90d9de442f1239d8fdfc96bd14900'
            'b831c43374da11e03831b150edb29d7cac2471561089a786686eb87ebb02eb88')

build() {
    cd "${srcdir}"/"${pkgname}-${pkgver}"/
    make
}

package() {
    cd "${srcdir}"/"${pkgname}-${pkgver}"/
    make DESTDIR="${pkgdir}" install
    chmod 750 "${pkgdir}"/usr/share/polkit-1/rules.d/
    rm "${pkgdir}"/usr/share/polkit-1/rules.d/90-org.opensuse.policykit.wihotspot.rules
    install -Dm 644 "${srcdir}"/org.opensuse.policykit.wihotspot.rules -t "${pkgdir}"/usr/share/polkit-1/rules.d/
    install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}
