# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>

pkgbase=ttf-maple
pkgname=('ttf-maple' 'otf-maple' 'woff2-maple' 'ttf-maple-nerd' 'ttf-maple-sc-nerd')
pkgver=6.2
pkgrel=1
pkgdesc='Open source monospace / Nerd-Font font with round corner and ligatures for IDE and command line.'
url='https://github.com/subframe7536/Maple-font/'
arch=('any')
license=('OFL')
source=($pkgname-$pkgver-MapleMono.zip::https://github.com/subframe7536/Maple-font/releases/download/v$pkgver/MapleMono.zip
        $pkgname-$pkgver-MapleMono-woff2.zip::https://github.com/subframe7536/Maple-font/releases/download/v$pkgver/MapleMono-woff2.zip
        $pkgname-$pkgver-MapleMono-otf.zip::https://github.com/subframe7536/Maple-font/releases/download/v$pkgver/MapleMono-otf.zip
        $pkgname-$pkgver-MapleMono-NF.zip::https://github.com/subframe7536/Maple-font/releases/download/v$pkgver/MapleMono-NF.zip
        $pkgname-$pkgver-MapleMono-SC-NF.zip::https://github.com/subframe7536/Maple-font/releases/download/v$pkgver/MapleMono-SC-NF.zip
        $pkgname-$pkgver-OFL.txt::https://raw.githubusercontent.com/subframe7536/Maple-font/v6.1/OFL.txt
        )
b2sums=('e32df9fabb79c99cb48f5622fc141d87b543190297fa5700eea8ce876bf9bd82c2f64aa1fb2fe0c55b9841c2c3d1817a5d7055d3b37694d99da5a25bfdf2f865'
        'c7ace0010564d646768541538ee4401ec21b49ded3bd28a04a61162ade219198b41d1589444383c8fa264d9f1072cd077db3aa7dc93112403b86fde3f9da2e39'
        'aa44a02da28dc88a7b130e9d9a5e312da46021ad89184d55192bef7a2e38ca92d84abb67f4b0ab9276e206963e0773e8d2317d5e503d4a36f63ec5bc8466249f'
        '7d8d722e28f3c4c43cdb41b6cc0795859aa8963bb4b59620e28c6967fe424318d1457c45d72c63b08b2e158c6f25b0469258e71153b0087a3b5149005a1cfa78'
        '12a05e29dd0f69899ff50f66a3c295974eee3e689c5e521ce9382b7391afafd028ea55b475280aa4261542139db6b6775a8cf92ad304e56bbaead7e042a7a324'
        '4c0d42227fd7722ef0a517a74c51cbee7ac4a0a9dce006de27f49195d22ced78f816e26f3623cfd0ef6214881f095a6977518d012cea41be41b6434dc4ec0825')

prepare() {
    mv $pkgname-$pkgver-OFL.txt OFL.txt
}

package_ttf-maple() {
    install -Dm644 -t "$pkgdir/usr/share/fonts/TTF" ttf/*.ttf
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" OFL.txt
}
package_otf-maple() {
    install -Dm644 -t "$pkgdir/usr/share/fonts/OTF" otf/*.otf
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" OFL.txt
}
package_woff2-maple() {
    install -Dm644 -t "$pkgdir/usr/share/fonts/WOFF2" woff2/*.woff2
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" OFL.txt
}
package_ttf-maple-nerd() {
    install -Dm644 -t "$pkgdir/usr/share/fonts/TTF" NF/*.ttf
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" OFL.txt
}
package_ttf-maple-sc-nerd() {
    install -Dm644 -t "$pkgdir/usr/share/fonts/TTF" SC-NF/*.ttf
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" OFL.txt
}
