# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>

pkgbase=ttf-maple
pkgname=('ttf-maple' 'otf-maple' 'woff2-maple' 'ttf-maple-nerd' 'ttf-maple-sc-nerd')
pkgver=6.3
pkgrel=1
pkgdesc='Open source monospace / Nerd-Font font with round corner and ligatures for IDE and command line.'
url='https://github.com/subframe7536/Maple-font/'
arch=('any')
license=('OFL')
source=($pkgname-$pkgver-MapleMono.zip::https://github.com/subframe7536/Maple-font/releases/download/v$pkgver/MapleMono.zip
        $pkgname-$pkgver-MapleMono-woff2.zip::https://github.com/subframe7536/Maple-font/releases/download/v$pkgver/MapleMono-woff2.zip
        $pkgname-$pkgver-MapleMono-otf.zip::https://github.com/subframe7536/Maple-font/releases/download/v$pkgver/MapleMono-otf.zip
        $pkgname-$pkgver-MapleMono-NF.zip::https://github.com/subframe7536/Maple-font/releases/download/v$pkgver/MapleMono-NF.zip
        $pkgname-$pkgver-MapleMono-SC-NF.zip::https://github.com/subframe7536/Maple-font/releases/download/v$pkgver/MapleMono-SC-NF.zip
        $pkgname-$pkgver-OFL.txt::https://raw.githubusercontent.com/subframe7536/Maple-font/v6.1/OFL.txt
        )
b2sums=('a302c51a4b483d852b5497464cd50c06279481580ba11b1514e6acdfefbc904c179bbe1ee5e35393c9669f138c3b469af9dc8b38edef2264eb22904c55317475'
        '0f364f24074ad8ab00f05af9d40ce9e4007657e377fdd96e70c62d76a4798387741dac914044e91b98d7d42f1c053c6a04b9a204fe09a074b6230f1127aca7ce'
        '47f2766b9642d814c6532ac6608e448cad6d999059d319828ef1ec82e63a16c6f7c93a3acb24d248866ac61921062f83c3700f17abecd8794066880aedcb7c73'
        '59ab719092fab7dfbb1235a275f0cd01cc902e4b58cf71899c0d426480d7925c744395f7c9898f44c3c5d19f037a9ae84139986e17e51d0fa18fdd3b6b4a48d1'
        '15ff73a1ce440675613afe9a2ce6b7d0ba8b06d56494414a0a45ec0cc4fc6286315f1716e88b39c1a16e1bae26df4dac962ef47c2385ce29c25996069174f2b9'
        '4c0d42227fd7722ef0a517a74c51cbee7ac4a0a9dce006de27f49195d22ced78f816e26f3623cfd0ef6214881f095a6977518d012cea41be41b6434dc4ec0825')

prepare() {
    mv $pkgname-$pkgver-OFL.txt OFL.txt
}

package_ttf-maple() {
    install -Dm644 -t "$pkgdir/usr/share/fonts/TTF" ttf/*.ttf
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" OFL.txt
}
package_otf-maple() {
    install -Dm644 -t "$pkgdir/usr/share/fonts/OTF" otf/*.otf
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" OFL.txt
}
package_woff2-maple() {
    install -Dm644 -t "$pkgdir/usr/share/fonts/WOFF2" woff2/*.woff2
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" OFL.txt
}
package_ttf-maple-nerd() {
    install -Dm644 -t "$pkgdir/usr/share/fonts/TTF" NF/*.ttf
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" OFL.txt
}
package_ttf-maple-sc-nerd() {
    install -Dm644 -t "$pkgdir/usr/share/fonts/TTF" SC-NF/*.ttf
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" OFL.txt
}
