# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Themaister <maister@archlinux.us>

pkgname=pcsx2-git
pkgver=1.7.3017
pkgrel=1
pkgdesc='A Sony PlayStation 2 emulator'
arch=(x86_64)
url=https://www.pcsx2.net
license=(
  GPL2
  GPL3
  LGPL2.1
  LGPL3
)
depends=(
  libaio
  libjpeg-turbo
  libpcap
  libzip
  libgl # For Steam Deck
  xorg-xrandr # For Steam Deck
  libxrender # For Steam Deck
  linux-api-headers # For Steam Deck
  libpulse
  portaudio
  libsamplerate
  sdl2
  wxgtk3
  fmt
  soundtouch
  wayland
  rapidyaml
  zstd
)
makedepends=(
  cmake
  git
  xorgproto
  ninja
  swig
  python
  vulkan-headers
)
install='pcsx2.install'
provides=('pcsx2')
conflicts=('pcsx2')
replaces=('pcsx2-avx-git' 'pcsx2-avx2-git')
source=('git+https://github.com/PCSX2/pcsx2.git')
sha256sums=('SKIP')

pkgver() {
  cd pcsx2

  git describe --tags | sed 's/^v//; s/-dev//; s/-/.r/; s/-g/./'
}

prepare() {
  cd pcsx2

  git submodule update --init --recursive
}

build() {
    cmake -S pcsx2 -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDOC_DIR=/usr/share/doc/pcsx2 \
    -DGAMEINDEX_DIR=/usr/share/pcsx2 \
    -DPLUGIN_DIR=/usr/lib/pcsx2 \
    -DDISABLE_ADVANCE_SIMD=ON \
    -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON \
    -DEXTRA_PLUGINS=ON \
    -DWAYLAND_API=ON \
    -DXDG_STD=TRUE \
    -DUSE_VULKAN=ON \
    -DUSE_SYSTEM_LIBS=ON \
    -DPACKAGE_MODE=ON

    ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}
# vim: ts=2 sw=2 et:
