# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Themaister <maister@archlinux.us>

pkgname=pcsx2-git
pkgver=1.7.3110
pkgrel=1
pkgdesc='A Sony PlayStation 2 emulator'
arch=(x86_64)
url=https://www.pcsx2.net
license=(
  GPL2
  GPL3
  LGPL2.1
  LGPL3
)

depends=(
  fmt
  libaio
  libjpeg-turbo
  libpcap
  libzip
  libgl
  xorg-xrandr
  libxrender
  linux-api-headers
  libpulse
  portaudio
  libsamplerate
  sdl2
  qt6-base
  qt6-wayland
  qt6-svg
  soundtouch
  wayland
  zstd
  rapidyaml
)
makedepends=(
  cmake
  git
  xorgproto
  ninja
  swig
  python
  vulkan-headers
  qt6-tools
)
install='pcsx2.install'
provides=('pcsx2')
conflicts=('pcsx2')
replaces=('pcsx2-avx-git' 'pcsx2-avx2-git')
source=('git+https://github.com/PCSX2/pcsx2.git')
sha256sums=('SKIP')

pkgver() {
  cd pcsx2

  git describe --tags | sed 's/^v//; s/-dev//; s/-/.r/; s/-g/./'
}

prepare() {
  cd pcsx2

  git submodule update --init --recursive

  sed -i 's@EmuFolders::Resources = Path::Combine(EmuFolders::AppRoot, "resources")@EmuFolders::Resources = Path::Combine("/usr/share", "PCSX2/resources")@g' pcsx2-qt/QtHost.cpp
  sed -i 's/env GDK_BACKEND=x11 MESA_NO_ERROR=1 pcsx2/pcsx2-qt/g' linux_various/PCSX2.desktop.in
}

build() {
    cmake -S pcsx2 -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_BUILD=ON \
    -DDISABLE_ADVANCE_SIMD=ON \
    -DPCSX2_APP_DATADIR=/usr/share \
    -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON \
    -DEXTRA_PLUGINS=ON \
    -DWAYLAND_API=ON \
    -DXDG_STD=TRUE \
    -DUSE_VULKAN=ON \
    -DUSE_SYSTEM_LIBS=ON \
    -DPACKAGE_MODE=ON \
    -DDISABLE_SETCAP=ON

    ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}
# vim: ts=2 sw=2 et:
