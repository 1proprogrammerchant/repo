# Maintainer: justforlxz <justforlxz@gmail.com>

pkgname=dtkgui-git
pkgver=5.5.24.r3.gd8f440c
pkgrel=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64' 'aarch64')
url="https://github.com/linuxdeepin/dtkgui"
license=('LGPL3')
depends=('dtkcore-git' 'librsvg' 'qt5-x11extras')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'dtkcore-git' 'librsvg' 'qt5-x11extras' 'gtest' 'gmock' 'doxygen')
conflicts=('dtkgui')
provides=('dtkgui')
groups=('deepin-git')
source=("$pkgname::git+https://github.com/linuxdeepin/dtkgui")
sha512sums=('SKIP')

pkgver() {
    cd $pkgname
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname
  cmake -GNinja \
    -DNOTPACKAGE=OFF \
    -DMKSPECS_INSTALL_DIR=/usr/lib/qt/mkspecs/modules/ \
    -DBUILD_DOCS=ON \
    -DQCH_INSTALL_DESTINATION=/usr/share/doc/qt \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  ninja
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
