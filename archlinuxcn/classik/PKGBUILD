# Maintainer: Rocket Aaron <i at rocka dot me>
# Contributor: Art Dev <artdevjs at gmail dot com>

pkgname=classik
pkgver=3.0.breeze5.23.80
pkgrel=2
pkgdesc='ClassiK is a highly customizable binary Window Decoration and Application Style plugin for recent versions of the KDE Plasma desktop. It provides the Classik, Kite, Oxygen/Breeze, and Redmond icon styles.'
arch=(x86_64)
url="https://github.com/paulmcauley/classik"
license=(GPL)
replaces=('classikstyles')
depends=(frameworkintegration kdecoration breeze-icons kwayland hicolor-icon-theme)
makedepends=(extra-cmake-modules kcmutils)
optdepends=('kcmutils: for classik-settings')
provides=()
conflicts=()
source=("${pkgname}-${pkgver}::${url}/archive/refs/tags/${pkgver}.tar.gz"
        "fix-gcc12-incomplete-type.patch::https://github.com/paulmcauley/classik/commit/ecaa3b7b305974fd79d0ea9fedd8fa4b6eab1730.patch")
sha256sums=('8258b06639e698b7e8a608afafae321574d911354b4dcc5043a1aab5929b18b4'
            'a07eaeecd99cdd3e26d56b93842515d0ec36a819a73858d6e9a8794bc25d6236')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix-gcc12-incomplete-type.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  cmake --build build
}

package() {
  cd "${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" cmake --install build
}

