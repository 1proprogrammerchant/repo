pkgname=devtools-arm-git
pkgver=0.0.0.13.4f2bb42
pkgrel=1
pkgdesc='Tools for Arch Linux ARM package maintainers'
arch=('any')
url='http://github.com/yuyichao/devtools-arm'
license=('GPL')
depends=('devtools')
makedepends=('cmake' 'git')
provides=('devtools-arm')
conflicts=('devtools-arm')
source=('git+https://github.com/yuyichao/devtools-arm')
sha256sums=('SKIP')
# People doing cross compilation with qemu and alike might want to override these
backup=(usr/share/devtools/setarch-aliases.d/armv6h
        usr/share/devtools/setarch-aliases.d/armv7h)

pkgver() {
  cd devtools-arm

  echo "0.0.0.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd devtools-arm
  mkdir -p build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd devtools-arm/build

  make DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}"/usr/share/devtools/setarch-aliases.d
  # The kernel calls all 32bit arm "arm".
  # setarch does recognize some aliases but they are all the same
  echo arm > "${pkgdir}"/usr/share/devtools/setarch-aliases.d/armv6h
  echo arm > "${pkgdir}"/usr/share/devtools/setarch-aliases.d/armv7h
}
