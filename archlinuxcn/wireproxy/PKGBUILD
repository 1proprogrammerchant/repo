# Maintainer: Ivan <vantu5z@mail.ru>
# Maintainer: Dct Mei <dctxmei@yandex.com>

pkgname=wireproxy
pkgver=1.0.5
pkgrel=1
pkgdesc="A wireguard client that exposes itself as a socks5 proxy or tunnels"
arch=('x86_64')
url="https://github.com/octeep/wireproxy"
license=('ISC')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "wireproxy.service"
        "wireproxy@.service"
        "wireproxy-user.service"
        "wireproxy-user@.service")
sha256sums=('3779a157713b462a7b5322f1437f768ab69ae446df0ceca76c6dff7c4030e425'
            '2ad52b7d158fef9c5bd468042e6c7cdc1130a3bde65015bee8107c0a6a422c0d'
            'e3f169d0f814c5c8d0e1a9d4af9ff7f86566f4239d00fffedb804c862202b264'
            '4e431b19a0e610e9d12c0ecc0ac9b0171659aaa74a696e93f2a5d4a7546ce85c'
            'a0b845b1e8bf6c6e931a5f574fb852997f58e32af6d30c766572357cab60624d')
b2sums=('fabc615678219cd18d5475f9ba354da1cf75a2981e33c8ea346f2e56046dbe6f9f1ad1e35139e5be91fd92e013b2c989784a2fd71d9901939b3dad78bfe68127'
        '07e3d3b429a0f4a584da6ee8871683e1739d8ed374a82ffec684b5eb3090ac2b97426100234e4dd76e07168d180aa733891e445ff31d9d33a589e7266e835d34'
        '04c00e7d73675e4b03ba60e2cbf5f26cff22e49dd7ee38be1fbcb0f6795ef8799cdca742c19ca10840875c4984c2cf461d13bba4eb88aebeecf737f9cb4478b2'
        'f33dfddceb6e1072d63890e65e9625249f70430fcd54dfb3eacf310569ad1a9a3aa2e112a33a0770ce7f9e3558c9d432cdc5f0f512513495fc76f85e792affa8'
        '0af9167adc3c09a11599b53443557ed85f4dc42ca80826d65237d630d1291b8398fc171ffcb9591042ac5862051e500178a3775047b265df6584343d5815a07c')

build() {
    cd "${srcdir}"/"${pkgname}-${pkgver}"/
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    go build -v \
       -ldflags="-X=main.version=${pkgver} -linkmode=external" \
       -o wireproxy ./cmd/wireproxy
}

check() {
    cd "${srcdir}"/"${pkgname}-${pkgver}"/
    go test -v ./...
}

package() {
    cd "${srcdir}"/"${pkgname}-${pkgver}"/
    install -dm0755 "${pkgdir}"/etc/"${pkgname}"/
    install -Dm0755 wireproxy -t "${pkgdir}"/usr/bin/
    install -Dm0644 LICENSE -t "${pkgdir}"/usr/share/licenses/"${pkgname}"/
    install -Dm0644 "${srcdir}"/wireproxy.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm0644 "${srcdir}"/wireproxy@.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm0644 "${srcdir}"/wireproxy-user.service "${pkgdir}"/usr/lib/systemd/user/wireproxy.service
    install -Dm0644 "${srcdir}"/wireproxy-user@.service "${pkgdir}"/usr/lib/systemd/user/wireproxy@.service
}
