# Maintainer: Butui Hu <hot123tea123@gmail.com>

_CUDA_ARCH_LIST="52;53;60;61;62;70;72;75;80;86"
pkgname=python-nvidia-dali
_pkgname=dali
pkgver=1.16.0
pkgrel=2
pkgdesc='A library containing both highly optimized building blocks and an execution engine for data pre-processing in deep learning applications'
arch=('x86_64')
url='https://github.com/NVIDIA/DALI'
license=('Apache')
depends=(
  cuda
  ffmpeg
  lmdb
  opencv
  python
)
makedepends=(
  clang
  cmake
  git
  nvidia-utils
  python-setuptools
)
options=(!emptydirs !lto)
source=("${pkgname}::git+https://github.com/NVIDIA/DALI.git#tag=v${pkgver}")
sha512sums=('SKIP')

get_pyver() {
  python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

prepare() {
  cd "${srcdir}/${pkgname}"
  git submodule update --init --recursive
  # quick fix for https://github.com/archlinuxcn/repo/issues/2877
  export CXXFLAGS=${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}
}

build() {
  cmake \
    -B "${srcdir}/build" \
    -DBUILD_BENCHMARK=OFF \
    -DBUILD_CUFILE=ON \
    -DBUILD_FFTS=ON \
    -DBUILD_LIBSND=ON \
    -DBUILD_LIBTAR=ON \
    -DBUILD_LIBTIFF=ON \
    -DBUILD_LMDB=ON \
    -DBUILD_NVDEC=ON \
    -DBUILD_NVJPEG2K=ON \
    -DBUILD_NVJPEG=ON \
    -DBUILD_NVML=ON \
    -DBUILD_NVOF=ON \
    -DBUILD_NVTX=OFF \
    -DBUILD_PROTOBUF=ON \
    -DBUILD_PYTHON=ON \
    -DBUILD_TEST=OFF \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCUDA_TARGET_ARCHS=${_CUDA_ARCH_LIST} \
    -DProtobuf_USE_STATIC_LIBS=OFF \
    -DVERBOSE_LOGS=ON \
    -DWITH_DYNAMIC_CUDA_TOOLKIT=ON \
    -S "${srcdir}/${pkgname}"
  make -C "${srcdir}/build"
  cd "${srcdir}/build/dali/python"
  python setup.py build
}

package() {
  make -C "${srcdir}/build" DESTDIR="${pkgdir}" install
  cd "${srcdir}/build/dali/python"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  # create softlink to save space
  ln -sf "/usr/lib/python$(get_pyver)/site-packages/nvidia/dali/libdali.so" "${pkgdir}/usr/lib/libdali.so"
  ln -sf "/usr/lib/python$(get_pyver)/site-packages/nvidia/dali/libdali_core.so" "${pkgdir}/usr/lib/libdali_core.so"
  ln -sf "/usr/lib/python$(get_pyver)/site-packages/nvidia/dali/libdali_kernels.so" "${pkgdir}/usr/lib/libdali_kernels.so"
  ln -sf "/usr/lib/python$(get_pyver)/site-packages/nvidia/dali/libdali_operators.so" "${pkgdir}/usr/lib/libdali_operators.so"
}
# vim:set ts=2 sw=2 et:
