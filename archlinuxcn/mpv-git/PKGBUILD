# Maintainer: Coelacanthus <CoelacanthusHex at gmail dot com>
# Contributor: DuckSoft <realducksoft at gmail dot com>

pkgname=mpv-git
_gitname=mpv
pkgver=0.34.0_308_gf235bfbf36
pkgrel=1
pkgdesc='A free, open source, and cross-platform media player (with all possible libs)'
arch=('x86_64')
license=('GPL3')
url='https://mpv.io/'
depends=('lcms2' 'libcdio-paranoia' 'libgl' 'libxss'
        'libxinerama' 'libxv' 'libxkbcommon' 'libva' 'wayland' 'libcaca'
        'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'luajit' 'mujs'
        'libdvdnav' 'libxrandr' 'jack' 'rubberband' 'uchardet' 'libarchive'
        'zlib' 'vapoursynth' 'sndio' 'vulkan-icd-loader' 'zimg' 'sdl2' 'libsixel'
        'ffmpeg' 'libavcodec.so' 'libavdevice.so' 'libavfilter.so' 'libavformat.so' 'libavutil.so' 'libswresample.so' 'libswscale.so'
        'libplacebo' 'libplacebo.so'
        'shaderc' 'libshaderc_shared.so'
)
makedepends=('git' 'mesa' 'python-docutils' 'vulkan-headers' 'wayland-protocols'
             'ffnvcodec-headers' 'meson' 'pipewire' 'pulseaudio' 'openal' 'alsa-lib'
)
optdepends=('youtube-dl: for video-sharing websites playback'
            'yt-dlp: for video-sharing websites playback'
            'openal: OpenAL audio output'
            'pipewire: PipeWire audio output'
            'pulseaudio: PulseAudio audio output'
            'alsa-lib: ALSA audio output'
            'smbclient: Samba support'
            'nvidia-utils: for hardware accelerated video decoding with CUDA')
provides=('mpv' 'libmpv.so')
conflicts=('mpv')
options=('!emptydirs')
source=("mpv::git+https://github.com/mpv-player/mpv")
sha256sums=('SKIP')
options=('debug' 'strip')

pkgver() {
    cd "$srcdir/mpv"
    ./version.sh | sed s/-/_/g
}

build() {
    local _opt_extra_flags=(
        '-Dgpl=true'
        '-Dcdda=enabled'
        '-Dlua=enabled' '-Dlua=luajit'
        '-Ddvbin=enabled'
        '-Dsdl2=enabled'
        '-Dspirv-cross=disabled'
    )
    
    local _other_os_flags=(
        '-Dmacos-10-11-features=disabled' # macOS
        '-Dmacos-10-12-2-features=disabled' # macOS
        '-Dmacos-10-14-features=disabled' # macOS
        '-Dmacos-cocoa-cb=disabled' # macOS
        '-Dmacos-media-player=disabled' # macOS
        '-Dmacos-touchbar=disabled' # macOS
        '-Dswift-build=disabled' # macOS
        '-Dswift-flags=disabled' # macOS
        '-Dgl-cocoa=disabled' # macOS
        '-Dcocoa=disabled' # macOS
        '-Daudiounit=disabled' # macOS
        '-Dvideotoolbox-gl=disabled' # macOS and iOS
        '-Dcoreaudio=disabled' # iOS
        '-Dios-gl=disabled' # iOS
        '-Dgl-dxinterop=disabled' # Windows
        '-Dd3d-hwaccel=disabled' # Windows
        '-Dd3d9-hwaccel=disabled' # Windows
        '-Dgl-dxinterop-d3d9=disabled' # Windows
        '-Dgl-win32=disabled' # Windows
        '-Degl-angle-win32=disabled' # Windows
        '-Degl-angle-lib=disabled' # Windows
        '-Degl-angle=disabled' # Windows
        '-Ddirect3d=disabled' # Windows
        '-Dd3d11=disabled' # Windows
        '-Dwasapi=disabled' # Windows
        '-Dwin32-internal-pthreads=disabled' # Windows
        '-Duwp=disabled' # Windows
        '-Dopensles=disabled' # Android
        '-Degl-android=disabled' # Android
        '-Doss-audio=disabled' # Not Linux
        '-Drpi-mmal=disabled' # RPI only
    )
    
    cd "$srcdir/mpv"
    arch-meson . build \
        --buildtype=debugoptimized \
        -Dlibmpv=true \
        -Ddefault_library=shared \
        "${_opt_extra_flags[@]}" \
        "${_other_os_flags[@]}"
    meson compile -C build
}

package() {
    cd "$srcdir/mpv"
    
    meson install -C build --no-rebuild --destdir="$pkgdir"
    
    install -D -m644 DOCS/{encoding.rst,tech-overview.txt} "${pkgdir}/usr/share/doc/mpv"
    install -D -m644 TOOLS/lua/* -t "${pkgdir}/usr/share/mpv/script"
}
