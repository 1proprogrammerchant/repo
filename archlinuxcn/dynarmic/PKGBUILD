# Maintainer: Alexandre Bouvier <contact@amb.tf>
pkgname=dynarmic
pkgver=6.4.6
pkgrel=1
pkgdesc="An ARM dynamic recompiler"
arch=('aarch64' 'x86_64')
url="https://github.com/merryhime/dynarmic"
license=('BSD')
depends=('llvm-libs' 'fmt' 'zydis')
makedepends=(
	'boost>=1.57'
	'cmake>=3.12'
	'llvm'
	'robin-map>=0.6.2'
	'xbyak>=6.68'
    'clang'
)
provides=('libdynarmic.so')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
b2sums=('8a1a1dc4e0b5d912c1403924786c7c13032c9386f8039ea01ba1296e810b9d4d7c1ed88f5c2c09b2be9d9c140b4efb71572a96fba000d0697401f450520921b2')

build() {
	cmake -S $pkgname-$pkgver -B build \
        -DCMAKE_CXX_COMPILER=clang++ \
        -DCMAKE_C_COMPILER=clang \
        -DCMAKE_C_FLAGS="$CFLAGS -flto=thin" \
        -DCMAKE_CXX_FLAGS="$CXXFLAGS -flto=thin" \
		-DBUILD_SHARED_LIBS=ON \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_CXX_FLAGS_RELEASE="-DNDEBUG" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DDYNARMIC_IGNORE_ASSERTS=ON \
		-DDYNARMIC_TESTS=OFF \
		-DDYNARMIC_USE_LLVM=ON \
		-DDYNARMIC_USE_PRECOMPILED_HEADERS=OFF \
		-Wno-dev
	cmake --build build
}

package() {
	# shellcheck disable=SC2154
	DESTDIR="$pkgdir" cmake --install build
	install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname $pkgname-$pkgver/LICENSE.txt
}
