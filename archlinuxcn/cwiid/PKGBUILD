# Maintainer: heavysink
pkgname=cwiid-git
_pkgname=cwiid
pkgver=svn_history
pkgrel=3
pkgdesc="Linux Nintendo Wiimote interface (GIT version)"
arch=("x86_64")
url="https://github.com/abstrakraft/${_pkgname}"
depends=("bluez-libs" "gtk2" "python2")
makedepends=("git")
license=("GPL2")
install="${_pkgname}.install"
source=("git+https://github.com/abstrakraft/${_pkgname}.git")
provides=('cwiid')
conflicts=('cwiid')
sha256sums=('SKIP')

prepare() {
  cd "${srcdir}/${_pkgname}"

  msg2 "Fixing ldconfig use in configure"
  sed -i "/ldconfig/s/WITH/ENABLE/" "${srcdir}/${_pkgname}/configure.ac"
  autoreconf -fi
}

build() {
  cd "${srcdir}/${_pkgname}"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-python=python2 \
    --enable-ldconfig=no
  LDFLAGS+=" -pthread -lpthread -lbluetooth" make -j1
}

package() {
  msg2 "Installing application"
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="${pkgdir}" install

  msg2 "Installing wminput README"
  install -Dm644 "${srcdir}/${_pkgname}/wminput/README" "${pkgdir}/usr/share/doc/${pkgname}/wminput/README"
}
