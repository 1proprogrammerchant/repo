# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: ThatOneCalculator <kainoa@t1c.dev>

_pkgname="hyprland"
pkgname="${_pkgname}-lily-git"
pkgver=0.23.0beta.r22.g6e697160
pkgrel=2
pkgdesc="A dynamic tiling Wayland compositor based on wlroots that doesn't sacrifice on its looks."
arch=(x86_64)
url="https://github.com/hyprwm/Hyprland"
license=('BSD')
# mesa is for wlroots-lily-git to select the [community] one for opengl-driver
depends=(cairo pango seatd wlroots-lily-git mesa)
makedepends=(git cmake ninja wayland-protocols python)
optdepends=('xorg-xwayland-lily: non-blurry HiDPI support for xwayland')
source=("${_pkgname}::git+https://github.com/hyprwm/Hyprland.git"
        git+https://github.com/hyprwm/hyprland-protocols.git
        git+https://github.com/canihavesomecoffee/udis86.git)
conflicts=("${_pkgname}")
provides=(hyprland)
sha256sums=(SKIP SKIP SKIP)

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^v//'
}

prepare() {
  cd "${srcdir}/${_pkgname}"
  git rm -r subprojects/wlroots
  git submodule init
  git config submodule.subprojects/hyprland-protocols.url "${srcdir}"/hyprland-protocols
  git config submodule.subprojects/udis86.url "${srcdir}"/udis86
  git -c protocol.file.allow=always submodule update
}

build() {
  cd "${srcdir}/${_pkgname}"

  make protocols

  cd subprojects/udis86
  mkdir -p build && cd build
  cmake -G Ninja -DCMAKE_BUILD_TYPE=RelWithDebInfo ..
  ninja

  cd "${srcdir}/${_pkgname}"
  sed -i 's/\S\+\.12032/wlroots/' CMakeLists.txt
  mkdir -p build && cd build
  cmake -G Ninja -DCMAKE_BUILD_TYPE=RelWithDebInfo -DCMAKE_INSTALL_PREFIX=/usr ..
  ninja

  cd "${srcdir}/${_pkgname}"
  cd hyprctl && make
}

package() {
  cd "${srcdir}/${_pkgname}"
  mkdir -p "${pkgdir}/usr/share/wayland-sessions"
  mkdir -p "${pkgdir}/usr/share/hyprland"
  mkdir -p "${pkgdir}/usr/share/man/man1"
  install -Dm755 build/Hyprland -t "${pkgdir}/usr/bin"
  install -Dm755 hyprctl/hyprctl -t "${pkgdir}/usr/bin"
  install -Dm644 assets/*.png -t "${pkgdir}/usr/share/hyprland"
  install -Dm644 docs/*.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm644 example/hyprland.desktop -t "${pkgdir}/usr/share/wayland-sessions"
  install -Dm644 example/hyprland.conf -t "${pkgdir}/usr/share/hyprland"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}"

  # plugin headers
  install -d "${pkgdir}/usr/include/hyprland/protocols"
  cp -R src "${pkgdir}/usr/include/hyprland/"
  find "${pkgdir}/usr/include/hyprland/" -type f ! -name '*.h*' -delete
  cp protocols/*-protocol.h  "${pkgdir}/usr/include/hyprland/protocols"
  install -Dm644 build/hyprland.pc -t "${pkgdir}/usr/share/pkgconfig"

  # deps hidden from devtools to avoid it trying to install conflicting packages
  _deps=(cairo pango seatd wlroots-lily-git libwlroots.so)
  depends=("${_deps[@]}")
}
