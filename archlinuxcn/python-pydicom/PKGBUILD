# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-pydicom
_pkgname=pydicom
pkgver=2.3.0
pkgrel=1
pkgdesc='Read, modify and write DICOM files with python code'
arch=(any)
url='https://github.com/pydicom/pydicom'
license=(MIT)
optdepends=(
  'python-numpy: for working with pixel data'
  'python-pillow: for working with compressed image data'
  'python-pylibjpeg'
  'python-pylibjpeg-openjpeg'
  'python-pylibjpeg-rle'
)
makedepends=(
  git
  python-setuptools
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pydicom/pydicom/archive/v${pkgver}.tar.gz")
sha512sums=('237b3d235e5819704b9b2082ee576053007a966ad72991b43df9017124352865edb5ae23f652822da9ea84a677061142b540b391d065583138dffa4c186d7f40')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
