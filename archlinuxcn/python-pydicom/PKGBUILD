# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-pydicom
_pkgname=pydicom
pkgver=2.2.2
pkgrel=2
pkgdesc='Read, modify and write DICOM files with python code'
arch=(any)
url='https://github.com/pydicom/pydicom'
license=(MIT)
optdepends=(
  'python-numpy: for working with pixel data'
  'python-pillow: for working with compressed image data'
  'python-pylibjpeg'
  'python-pylibjpeg-openjpeg'
  'python-pylibjpeg-rle'
)
makedepends=(
  git
  python-setuptools
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pydicom/pydicom/archive/v${pkgver}.tar.gz")
sha512sums=('6227bd29819f70f5e1263c3523e4c9abb4892b7f82a2f1f2993b1fc8898e3811f4097397eba6579a783622760e4a9424d3acceacf1d14a351cb92f34d83da779')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
