# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-nibabel
_pkgname=nibabel
pkgver=4.0.2
pkgrel=2
pkgdesc='Package to access a cacophony of neuro-imaging file formats'
arch=('any')
url='http://nipy.org/nibabel'
license=('MIT')
depends=(
  python-numpy
  python-pillow
  python-pydicom
  python-scipy
  python-six
)
checkdepends=(
  python-pytest
)
makedepends=(
  python-build
  python-hatch-vcs
  python-hatchling
  python-installer
  python-setuptools
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/nipy/nibabel/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('71e1453ab897ef228062bbf9eae4430a0e89c024d110d68ac70414e4cdf02dafbbe3f0f640ef8b1adfe59c9171839d3f398b34b6bcd39aa4826960135187d3cd')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${_pkgname}-${pkgver}"
  # export NIPY_EXTRA_TESTS=slow
  pytest -v nibabel
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
