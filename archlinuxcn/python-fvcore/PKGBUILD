# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Jerry Lin <jerry73204 at gmail dot com>

_pkgname=fvcore
pkgname=python-fvcore
pkgver=0.1.5.post20210518
pkgrel=1
pkgdesc="Collection of common code that's shared among different research projects in FAIR computer vision team."
arch=('any')
url="https://github.com/facebookresearch/fvcore"
license=('Apache')
makedepends=(python-setuptools)
depends=(python)
source=("${_pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('bfface25eb017011324e0c4649440591178c6ba72d0d0719255f8f7828ba8941')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
# vim:set ts=2 sw=2 et:
