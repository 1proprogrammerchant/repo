# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Leo Mao <leomaoyw at gmail dot com>

pkgname=python-fastrlock
_pkgname=fastrlock
pkgver=0.8
pkgrel=2
pkgdesc='A fast RLock implementation for CPython'
url='https://github.com/scoder/fastrlock'
arch=('x86_64')
license=('MIT')
depends=(
  glibc
)
makedepends=(
  cython
  python-setuptools
)
checkdepends=(
  python-pytest
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/scoder/fastrlock/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('88992a7db4e42366055bf9b1b7bab23e60e04fad5acc97286372a5baaaf8b2f19ba9ce65b8ba284680fb30f576ae26ed9cee2bbb33ffa0fd069ab9ba41cb45a9')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
  python setup.py build_ext --inplace
}

check() {
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  cd "${_pkgname}-${pkgver}"
  PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-cpython-${python_version}" pytest -v
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
