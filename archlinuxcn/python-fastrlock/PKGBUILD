# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Leo Mao <leomaoyw at gmail dot com>

pkgname=python-fastrlock
_pkgname=fastrlock
pkgver=0.8.1
pkgrel=2
pkgdesc='A fast RLock implementation for CPython'
url='https://github.com/scoder/fastrlock'
arch=('x86_64')
license=('MIT')
depends=(
  glibc
)
makedepends=(
  cython
  python-setuptools
)
checkdepends=(
  python-pytest
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/scoder/fastrlock/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('ecd7fd2ad6d090bfeadf9c5c885645b7cee3600108cf033773d6ba3c4cf2a7db69c2c97ab8eabab08c198645fd757e481444bded03edc91dc60cec3d094b50f8')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
  python setup.py build_ext --inplace
}

check() {
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  cd "${_pkgname}-${pkgver}"
  PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-cpython-${python_version}" pytest -v
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
