# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=PyPDF2
pkgname=python-pypdf2
pkgver=2.10.1
pkgrel=1
pkgdesc='A utility to read and write PDFs with Python'
arch=('any')
url='https://mstamy2.github.io/PyPDF2'
license=('BSD')
depends=(
  python
)
makedepends=(
  python-setuptools
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/mstamy2/PyPDF2/archive/${pkgver}.tar.gz")
sha512sums=('a9869daf04c7a399f4404386d3a313f4a5ce31de1fcc54ede04276c51517f7fcdadb1645319b8b5f16aa1969ac9f8fb6d5d2357467506949ddef77c63b63686d')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
