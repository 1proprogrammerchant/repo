# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=PyPDF2
pkgname=python-pypdf2
pkgver=2.10.4
pkgrel=1
pkgdesc='A utility to read and write PDFs with Python'
arch=('any')
url='https://mstamy2.github.io/PyPDF2'
license=('BSD')
depends=(
  python
)
makedepends=(
  python-setuptools
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/mstamy2/PyPDF2/archive/${pkgver}.tar.gz")
sha512sums=('ac865096ae370626ad7ab243a5c95fafc95e71c31e79c4f89be919205d956726d0147a7b4cf5a2bcddb021999261fa0a73942df64763cf3c8b30620274ea518d')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
