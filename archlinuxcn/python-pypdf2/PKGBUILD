# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=PyPDF2
pkgname=python-pypdf2
pkgver=1.27.5
pkgrel=1
pkgdesc='A utility to read and write PDFs with Python'
arch=('any')
url='https://mstamy2.github.io/PyPDF2'
license=('BSD')
depends=(
  python
)
makedepends=(
  python-setuptools
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/mstamy2/PyPDF2/archive/${pkgver}.tar.gz")
sha512sums=('c1f77c7ea46e866da104cf05421ccf15870db07923ff5860a6b44c944d599b38e98d33d6042a155b0698ecd100d1c6f57aa7bb8904dd08e679f6560a578d4488')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
