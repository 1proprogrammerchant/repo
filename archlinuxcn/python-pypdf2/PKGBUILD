# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=PyPDF2
pkgname=python-pypdf2
pkgver=2.12.0
pkgrel=1
pkgdesc='A utility to read and write PDFs with Python'
arch=('any')
url='https://mstamy2.github.io/PyPDF2'
license=('BSD')
depends=(
  python
)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/mstamy2/PyPDF2/archive/${pkgver}.tar.gz")
sha512sums=('dff1225ec72e4d27d3896cbc80d013bade641be5a56d4f95b24c40544082da55c8568034fc4b9d6724eadd10c2754d1257d9e09727f7981fcdda0fcf0948bad5')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
