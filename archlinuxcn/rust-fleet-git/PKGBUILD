# Maintainer: Y7n05h <Y7n05h(aT)protonmail--d0t--com>
# Contributor: icepie <icepie.dev@gmail.com>

pkgname="rust-fleet-git"
_pkgname="fleet"
pkgver=0.0.8.142.g9855e5f
pkgrel=1
pkgdesc="The blazing fast build tool for Rust."
arch=("x86_64")
url="https://github.com/dimensionhq/fleet"
license=("MIT")
depends=('rust-nightly' 'cargo-nightly' 'lld' 'clang' 'sccache')
optdepends=()
makedepends=("git")
provides=("rust-fleet")
conflicts=("fleet-rust-bin" "fleet-rust-git" "rust-fleet")
replaces=("fleet-rust-git")
source=("$_pkgname::git+https://github.com/dimensionhq/fleet.git")
sha256sums=("SKIP")

pkgver() {
    cd "$_pkgname"
    echo "$(grep '^version =' Cargo.toml | head -n1 | cut -d\" -f2).$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
    cd $_pkgname
    cargo build --release
}

check() {
    cd $_pkgname
    cargo test --release
}

package() {
    cd "$srcdir/$_pkgname"
    install -Dm755 target/release/$_pkgname "$pkgdir/usr/bin/$_pkgname"
}
