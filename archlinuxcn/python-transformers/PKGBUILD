# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=transformers
pkgname=python-transformers
pkgver=4.20.0
pkgrel=1
pkgdesc='State-of-the-art Machine Learning for Pytorch, TensorFlow, and JAX'
arch=('any')
url='https://github.com/huggingface/transformers'
license=('Apache')
depends=(
  python-filelock
  python-huggingface-hub
  python-numpy
  python-regex
  python-requests
  python-sacremoses
  python-tokenizers
  python-tqdm
  python-yaml
)
makedepends=(
  python-setuptools
)
optdepends=(
  python-pytorch
  python-tensorflow
)

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/huggingface/transformers/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('3866975fb522d5ac6c2249a80b1dddfba6fdc196247cdb7c02ac6a23caacaad7a4e6b85defc86618ee8dd5eabf5f8334f469d7d917f50296bbeca0e64795c56b')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
# vim:set ts=2 sw=2 et:
