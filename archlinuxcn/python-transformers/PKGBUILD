# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=transformers
pkgname=python-transformers
pkgver=4.15.0
pkgrel=1
pkgdesc='State-of-the-art Machine Learning for Pytorch, TensorFlow, and JAX'
arch=('any')
url='https://github.com/huggingface/transformers'
license=('Apache')
depends=(
  python-filelock
  python-huggingface-hub
  python-numpy
  python-regex
  python-requests
  python-sacremoses
  python-tokenizers
  python-tqdm
  python-yaml
)
makedepends=(
  python-setuptools
)
optdepends=(
  python-pytorch
  python-tensorflow
)

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/huggingface/transformers/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('6dc4f7b15b038b00946a22b8ca664275fa85ed60b47c25cc2cbaed50514156e16abc5deaae6185da62dbca990cfdc9d0703650d5101a99cb1d42a611fe856abf')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
# vim:set ts=2 sw=2 et:
