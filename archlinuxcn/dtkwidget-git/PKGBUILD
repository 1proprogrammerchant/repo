# Maintainer: justforlxz <justforlxz@gmail.com>

pkgname=dtkwidget-git
pkgver=5.5.46.r9.gf0e1eced
pkgrel=1
pkgdesc='Deepin graphical user interface library'
arch=('x86_64' 'aarch64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('LGPL3')
depends=('deepin-qt-dbus-factory-git' 'dtkcore-git' 'dtkgui-git' 'librsvg' 'qt5-multimedia' 'qt5-svg'
         'qt5-x11extras' 'startup-notification')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'gtest' 'dtkcommon-git' 'dtkcore-git' 'dtkgui-git' 'doxygen')
provides=('dtkwidget')
conflicts=('dtkwidget')
groups=('deepin-git')
source=("$pkgname::git+https://github.com/linuxdeepin/dtkwidget.git")
sha512sums=('SKIP')

pkgver() {
    cd $pkgname
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname
  cmake -GNinja \
      -DHUNTER_ENABLED=OFF \
      -DMKSPECS_INSTALL_DIR=/usr/lib/qt/mkspecs/modules/\
      -DBUILD_PLUGINS=OFF \
      -DBUILD_DOCS=ON \
      -DQCH_INSTALL_DESTINATION=/usr/share/doc/qt \
      -DCMAKE_INSTALL_LIBDIR=/usr/lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release 
  ninja
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
