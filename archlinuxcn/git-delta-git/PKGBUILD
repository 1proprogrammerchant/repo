# Maintainer: zjuyk <ownbyzjuyk@gmail.com>

pkgname=git-delta-git
pkgver=0.8.4.20210902
pkgrel=1
pkgdesc="A syntax-highlighting pager for git"
arch=('any')
url="https://github.com/dandavison/delta"
license=('MIT')
makedepends=('git' 'rust' 'cargo')
provides=(git-delta=$pkgver)
source=(git+$url)
md5sums=('SKIP')

pkgver() {
  cd delta
  echo $(grep -oPm1 '(?<=^version = ")[^"]+' Cargo.toml).$(TZ=UTC git log -1 --pretty='%cd' --date=short-local | tr -d '-')
}

build() {
  cd delta
  sed -i '/profile.release/alto = true' Cargo.toml
  cargo build --release
  git checkout Cargo.toml
}

check() {
  cd delta
  cargo test --release --locked
}

package() {
  cd delta

  install -Dm755 "target/release/delta" "$pkgdir/usr/bin/delta"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
