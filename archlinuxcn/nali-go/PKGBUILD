# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>

pkgname=nali-go
pkgver=0.4.7
pkgrel=1
pkgdesc='An offline tool for querying IP geographic information and CDN provider.'
arch=('x86_64')
url="https://github.com/zu1k/nali"
license=('MIT')
conflicts=('nali-cli')
makedepends=('go')
source=("$pkgname-$pkgver::https://github.com/zu1k/nali/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('a1116c889f423a29b2c9e3ae84fe7787e80538a2ad1e6f4b6be31db5d4f7a0e7')
b2sums=('d192d83594fa24c295b2cb642f8b9658a1c7898ac4126259f5761849a811cd0a2755379ac6f9f28d351c2c5ca1f2c361a0ecf7b8b6711e8e4030f38d2a6250be')
# Golang no support
options=(!lto)

build() {
    cd "$srcdir/nali-$pkgver"
    go build \
        -trimpath \
        -buildmode=pie \
        -mod=readonly \
        -modcacherw \
        -ldflags "-linkmode external -X \"github.com/zu1k/nali/constant.Version=$pkgver\" -extldflags $LDFLAGS" \
        -o $pkgname .
}

package() {
    cd "$srcdir/nali-$pkgver"
    install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
    ln -sf ./$pkgname "$pkgdir"/usr/bin/nali
    mkdir -p "$pkgdir/usr/share/bash-completion/completions/"
    ./$pkgname completion bash > "$pkgdir/usr/share/bash-completion/completions/nali"
    mkdir -p "$pkgdir/usr/share/zsh/site-functions/"
    ./$pkgname completion zsh > "$pkgdir/usr/share/zsh/site-functions/_nali"
    mkdir -p "$pkgdir/usr/share/fish/completions/"
    ./$pkgname completion fish > "$pkgdir/usr/share/fish/completions/nali.fish"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
