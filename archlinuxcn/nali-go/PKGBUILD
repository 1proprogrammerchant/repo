# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>

pkgname=nali-go
pkgver=0.4.1
pkgrel=1
pkgdesc='An offline tool for querying IP geographic information and CDN provider.'
arch=('x86_64')
url="https://github.com/zu1k/nali"
license=('MIT')
conflicts=('nali-cli')
makedepends=('go')
source=("$pkgname-$pkgver::https://github.com/zu1k/nali/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('4bbfd5f174516ccb2a02a3e3e689aad26a1c6f7244c3b849b010103cd0ac0ee5')
b2sums=('fb16c61d6f1e6bb86ce8ae236491108226bc7aecea429e7ab7309c3eab24c3fa5b850862d8cf05abc7ab31b8e766f0d420c587c11a56368b80f9b0e55d61b739')
# Golang no support
options=(!lto)

build() {
    cd "$srcdir/nali-$pkgver"
    go build \
        -trimpath \
        -buildmode=pie \
        -mod=readonly \
        -modcacherw \
        -ldflags "-linkmode external -X \"github.com/zu1k/nali/constant.Version=$pkgver\" -extldflags $LDFLAGS" \
        -o $pkgname .
}

package() {
    cd "$srcdir/nali-$pkgver"
    install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
    ln -sf ./$pkgname "$pkgdir"/usr/bin/nali
    mkdir -p "$pkgdir/usr/share/bash-completion/completions/"
    ./$pkgname completion bash > "$pkgdir/usr/share/bash-completion/completions/nali"
    mkdir -p "$pkgdir/usr/share/zsh/site-functions/"
    ./$pkgname completion zsh > "$pkgdir/usr/share/zsh/site-functions/_nali"
    mkdir -p "$pkgdir/usr/share/fish/completions/"
    ./$pkgname completion fish > "$pkgdir/usr/share/fish/completions/nali.fish"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
