# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>

pkgname=nali-go
pkgver=0.4.8
pkgrel=1
pkgdesc='An offline tool for querying IP geographic information and CDN provider.'
arch=('x86_64')
url="https://github.com/zu1k/nali"
license=('MIT')
conflicts=('nali-cli')
makedepends=('go')
source=("$pkgname-$pkgver::https://github.com/zu1k/nali/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('ce6a0be171839640634047f90fb40eafda17dd4439329df0caf110ce186bfc91')
b2sums=('22a77708e3611eaee106ce7a8b776e6ad1bf16eab3d86730dad4d08943e776aca1fc235ce0d097f2dc963ee9551db0664aa089a415803beb94dcac116d562530')
# Golang no support
options=(!lto)

build() {
    cd "$srcdir/nali-$pkgver"
    go build \
        -trimpath \
        -buildmode=pie \
        -mod=readonly \
        -modcacherw \
        -ldflags "-linkmode external -X \"github.com/zu1k/nali/constant.Version=$pkgver\" -extldflags $LDFLAGS" \
        -o $pkgname .
}

package() {
    cd "$srcdir/nali-$pkgver"
    install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
    ln -sf ./$pkgname "$pkgdir"/usr/bin/nali
    mkdir -p "$pkgdir/usr/share/bash-completion/completions/"
    ./$pkgname completion bash > "$pkgdir/usr/share/bash-completion/completions/nali"
    mkdir -p "$pkgdir/usr/share/zsh/site-functions/"
    ./$pkgname completion zsh > "$pkgdir/usr/share/zsh/site-functions/_nali"
    mkdir -p "$pkgdir/usr/share/fish/completions/"
    ./$pkgname completion fish > "$pkgdir/usr/share/fish/completions/nali.fish"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
