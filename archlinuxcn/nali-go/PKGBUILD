# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>

pkgname=nali-go
pkgver=0.4.2
pkgrel=1
pkgdesc='An offline tool for querying IP geographic information and CDN provider.'
arch=('x86_64')
url="https://github.com/zu1k/nali"
license=('MIT')
conflicts=('nali-cli')
makedepends=('go')
source=("$pkgname-$pkgver::https://github.com/zu1k/nali/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('25842823d61b1c05d8e261c28a2f24232838a1397cbe3b227a2c6288ec451fd6')
b2sums=('5c84e39badd20946af698199d7a17d9a27e323a94bdc4247bf95bc225fbb5922bf30a02b650a9fc4f46e6db331c083a73e4148e9f7fef378562e3b1f7bb308d7')
# Golang no support
options=(!lto)

build() {
    cd "$srcdir/nali-$pkgver"
    go build \
        -trimpath \
        -buildmode=pie \
        -mod=readonly \
        -modcacherw \
        -ldflags "-linkmode external -X \"github.com/zu1k/nali/constant.Version=$pkgver\" -extldflags $LDFLAGS" \
        -o $pkgname .
}

package() {
    cd "$srcdir/nali-$pkgver"
    install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
    ln -sf ./$pkgname "$pkgdir"/usr/bin/nali
    mkdir -p "$pkgdir/usr/share/bash-completion/completions/"
    ./$pkgname completion bash > "$pkgdir/usr/share/bash-completion/completions/nali"
    mkdir -p "$pkgdir/usr/share/zsh/site-functions/"
    ./$pkgname completion zsh > "$pkgdir/usr/share/zsh/site-functions/_nali"
    mkdir -p "$pkgdir/usr/share/fish/completions/"
    ./$pkgname completion fish > "$pkgdir/usr/share/fish/completions/nali.fish"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
