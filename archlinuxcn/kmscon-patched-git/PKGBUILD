# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: Aetf <7437103@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: David Herrmann <dh.herrmann@googlemail.com>

pkgname=kmscon-patched-git
_pkgname=kmscon
pkgver=9.0.0.r7.ga95c9ae
pkgrel=1
pkgdesc='Terminal emulator based on Kernel Mode Setting (KMS) (forked and patched version)'
arch=('x86_64' 'armv7h' 'aarch')
url='https://github.com/Aetf/kmscon'
license=('MIT')
depends=(systemd libdrm mesa libgl pango libxkbcommon xkeyboard-config libtsm-patched)
makedepends=(meson libxslt docbook-xsl linux-api-headers git)
options=(debug strip)
provides=(kmscon)
conflicts=(kmscon)
source=("git+https://github.com/Aetf/kmscon.git")
md5sums=('SKIP')

pkgver() {
    cd $srcdir/$_pkgname
    git describe --long | sed -r "s/^$_pkgname-//;s/^v//;s/([^-]*-g)/r\\1/;s/-/./g"
}

prepare() {
    cd $srcdir/$_pkgname

    arch-meson . build \
        --buildtype=debugoptimized
}

build() {
    cd $srcdir/$_pkgname

    meson compile -C build
}

package() {
    cd $srcdir/$_pkgname

    meson install -C build --no-rebuild --destdir="$pkgdir"

    mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
    install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}
