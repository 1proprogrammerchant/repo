# Maintainer: grufo <madmurphy333 at gmail.com>

pkgname='proot-git'
pkgver='r1263.8d94d2a'
pkgrel=1
pkgdesc='chroot, mount --bind, and binfmt_misc without privilege/setup'
arch=(armv7h aarch64 i686 x86_64)
url='http://proot.me'
license=('GPL')
provides=('proot')
conflicts=('proot' 'proot-bin')
depends=('talloc')
makedepends=('git' 'python-docutils' 'libxslt')
source=("git+https://github.com/proot-me/PRoot")
sha1sums=('SKIP')

pkgver() {
  cd PRoot
  printf "'r%s.%s'" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd PRoot
}

build() {
  make -C PRoot/src -f GNUmakefile

  ln -s ../../CHANGELOG.rst PRoot/doc/proot/changelog.rst
  ln -s ../../CHANGELOG.rst PRoot/doc/care/changelog.rst
  make -C PRoot/doc -f GNUmakefile
}

package() {
  cd PRoot

  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 "src/proot" "${pkgdir}/usr/bin"

  install -m755 -d "${pkgdir}/usr/share/man/man1/"
  install -m644 -T 'doc/proot/man.1' "${pkgdir}/usr/share/man/man1/proot.1"

  install -m755 -d "${pkgdir}/usr/share/doc/proot/"
  install -m644 'doc/proot/changelog.rst' "${pkgdir}/usr/share/doc/proot"
  install -m644 'doc/proot/index.html' "${pkgdir}/usr/share/doc/proot"
  install -m644 'doc/proot/manual.rst' "${pkgdir}/usr/share/doc/proot"

  install -m755 -d "${pkgdir}/usr/share/doc/proot/stylesheets"
  install -m644 'doc/proot/stylesheets/'* "${pkgdir}/usr/share/doc/proot/stylesheets"
}
